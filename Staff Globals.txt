&CMD`LONGCHECKPL [u(cobj,staff_globals)]=$+longcheckpl:think u(setq,c,iter(u(setr,l,search(all,type,player,elock,level:>34&!ooc_character:1&!flag^wizard&%(adminattrs`ic_character:1|!flag^royalty%))),get(%i0/altinfo`caltsid))); think u(setq,s,iter(setunion(%qc,,,n),elements(%ql,matchall(%qc,%i0)),,|)); think pemit(%#,repeat(=,50)); think iter(%qs,pemit(%#,[ljust(name(first(%i0)),20)][u(setq,0,iter(%i0,get(%i0/level)))][iter(setunion(%q0,,,n),%i0 - [words(matchall(%q0,%i0))],,%,%b)]),|); think pemit(%#,repeat(=,50));

&CMD`+PLSEARCH [u(cobj,staff_globals)]=$+plsearch *:@pemit %#=[u(header,Level %0)]%r[u(table,iter(u(setr,l,sort(search(All,type,player,elock,level:%0),name)),name(%i0),,|),26,78,|,)]%r[u(header,Total: [words(%ql)])]

&CMD`+CALTS [u(cobj,staff_globals)]=$+calts *:@force/inline %#=+account %0
&CMD`+MALTS [u(cobj,staff_globals)]=$+malts *:@force/inline %#=+account %0

&CMD`CHECKPC [u(cobj,staff_globals)]=$+checkpc *:@break findplayer(%0); @pemit %#=ansi(h,[ansi(c,name(%qp))] is [if(u(setr,l,itemize(iter(checkpc(%qp),ansi(hr,%i0)))),missing %ql,ansi(hg,OK))].)

&CMD`LISTIDLE [u(cobj,staff_globals)]=$+listidle:think [u(setq,0,sub(secs(),mul(60,60,24,120)))][u(setq,1,sub(secs(),mul(60,60,24,30)))][u(setq,2,sub(secs(),mul(60,60,24,60)))]; think [u(setq,v,sort(search(all,type,player,flags,o!c,elock,!%(idle_character:1|ooc_character:1%)&lastseconds:<%q0),attr:lastseconds))]; think [u(setq,f,sort(search(all,type,player,flags,!o!c,elock,!%(idle_character:1|ooc_character:1%)&chartype:FC&lastseconds:<%q1),attr:lastseconds))]; think [u(setq,o,sort(search(all,type,player,flags,!o!c,elock,!%(idle_character:1|ooc_character:1%)&chartype:OC&lastseconds:<%q2),attr:lastseconds))]; think [u(setq,c,sort(search(all,type,player,flags,!o!c,elock,!%(idle_character:1|ooc_character:1%)&chartype:OFC&lastseconds:<%q2),attr:lastseconds))]; think u(setq,s,secs())[pemit(%#,center(< FC >,78,=))][iter(%qf,pemit(%#,ljust(name(%i0),22)[rjust([first(timestring(sub(%qs,get(%i0/lastseconds))))],6)] %b%b[get(%i0/series)]))][pemit(%#,center(< OFC >,78,=))][iter(%qc,pemit(%#,ljust(name(%i0),22)[rjust([first(timestring(sub(%qs,get(%i0/lastseconds))))],6)] %b%b[get(%i0/series)]))][pemit(%#,center(< OC >,78,=))][iter(%qo,pemit(%#,ljust(name(%i0),22)[rjust([first(timestring(sub(%qs,get(%i0/lastseconds))))],6)] %b%b[get(%i0/series)]))][pemit(%#,center(< Vacation >,78,=))][iter(%qv,pemit(%#,ljust(name(%i0),22)[rjust([first(timestring(sub(%qs,get(%i0/lastseconds))))],6)] %b%b[get(%i0/series)]))][pemit(%#,repeat(-,78))];@pemit/silent %#=[center(Rules: FC 30 days%, OC/OFC 60 days%, Vacation 120 days,78)]%r[center(Characters with attributes 'idle_character' or 'ooc_character' are ignored,78)]%r[repeat(=,78)]

&CMD`PLSTATS [u(cobj,staff_globals)]=$+plstats:@assert u(isadmin,%#);think u(setq,l,search(all,type,player,elock,level:>0)); think [u(setq,0,0)][u(setq,s,sort(u(setr,p,iter(%ql,get(%i0/level)))))][u(setq,t,iter(setunion(%qs,%qs),%i0: [u(setr,w,words(matchall(%qs,%i0)))][if(gt(%qw,%q0),u(setq,0,%qw)[u(setq,o,%i0)])],,|))]; @pemit/silent %#=%r[ljust(Mean:,7)] [lmath(mean,%qs)]%r[ljust(Stddev:,7)] [lmath(stddev,%qs)]%r[ljust(Mode:,7)] %qo%r[ljust(Median:,7)] [lmath(median,%qs)]%r%r[u(table,%qt,10,44,|)]%r

&CMD`+SCRY [u(cobj,staff_globals)]=$+scry *:@break if(hastype(u(setr,t,pmatch(%0)),player),0,pemit(%#,That is not a valid player.)1); @pemit %#=center(Combat Status of [name(%qt)],44,=)%r| %b %b %b %bHP: [ljust(u(#1247/uthp_now)/[u(#1247/uthp_max)],7)] [SPACE(6)] Status: [switch(1,lte(u(#1247/uthp_now),0),KOed,getsta(%qt,stun),Stun,Okay)] %b |%r| %b %b Skill: [ljust(getnum(%qt,ski),2)] [SPACE(10)] Evasion: [ljust(getnum(%qt,eva),2)] %b %b |%r|%b Strength: [ljust(getnum(%qt,str),2)] [SPACE(8)] Endurance: [ljust(getnum(%qt,end),2)] %b %b |%r| %b %b %b Aid: [ljust(getnum(%qt,aid),2)][space(14)]Level: [ljust(u(#1247/u_lvl,%qt),5)]%b%b|%r| %b %b +miss: [if(getsta(%#,miss),No%b,Yes)] [SPACE(10)] +allow: [if(u(uallow),Yes,No%b)] %b %b|%r|[center([u(setq,x,default(%qt/COMBAT`DEANDBUFFDUR,0))]Buffed : [if(gt(%qx,0),BUFFED for %qx actions.)][if(lt(%qx,0),DEBUFFED for [mul(-1,%qx)] actions.)][if(eq(%qx,0),No.)],42)]|%r|[center(Epic Buffs : [switch([default(*%0/combat`ubuff-att,0)][default(*%0/combat`ubuff-def,0)],11,Both,10,Attack,01,Defense,None)],42)]|%r|[center(Signature and Other Info,42,-)]|%r|[center(Attack Buff : [mul(100,[default([pmatch(%0)]/combat`legacy`tdata`sgatkbuff,0)])]%%,21,)][center(Defense Buff : [mul(100,[default([pmatch(%0)]/combat`legacy`tdata`sgDefbuff,0)])]%%,21,)]|%r|[center(Buff Falloff : [mul(100,[default([pmatch(%0)]/combat`legacy`tdata`sgbufffall,0)])],22,)][center([if([default([pmatch(%0)]/combat`authority,0)],[ansi(hy,<<A>>)],[ansi(hx,<<A>>)])],10)][center([if([default([pmatch(%0)]/combat`legacy`bosson,0)],[ansi(hr,<<B>>)],[ansi(hx,<<B>>)])],10)]|%r[if([default([pmatch(%0)]/combat`authority,0)],|[center(Authority Mode:[space(5)] Global: [mul(100,[u([pmatch(%0)]/combat`authority)])]%%,42)]|%r)][if([default([pmatch(%0)]/combat`legacy`bosson,0)],|[center(Boss Mode:[space(5)]Attack: [mul([u([pmatch(%0)]/combat`legacy`bossatk)],100)]%% Defense: [mul([u([pmatch(%0)]/combat`legacy`bossdef)],100)]%%,42)]|%r)][repeat(=,44)]

&CMD`+SETMAXSTATS [u(cobj,staff_globals)]=$+setmaxstats *=*:@break not(u(isadmin,%#)); @break findplayer(%0); @break case(1,[strmatch(%1,clear)],pemit(%#,ansi(h,Maxstats cleared for [name(%qp)].))[wipe(%qp/stats`max*)]1,[regmatch(%1,^(%\d+) (%\d+) (%\d+) (%\d+) (%\d+)$,-1 0 1 2 3 4)],0,pemit(%#,ansi(r,Invalid arguments. Provide 5 numbers separated by spaces.))1); @pemit %#=ansi(h,Set maxstats for [name(%qp)]. Skill: [ansi(hx,%q0)] Strength: [ansi(hx,%q1)] Evasion: [ansi(hx,%q2)] Endurance: [ansi(hx,%q3)] Aid: [ansi(hx,%q4)]); think [set(%qp,stats`maxskill:%q0)][set(%qp,stats`maxstrength:%q1)][set(%qp,stats`maxevasion:%q2)][set(%qp,stats`maxendurance:%q3)][set(%qp,stats`maxaid:%q4)]

&CMD`+SETMINSTATS [u(cobj,staff_globals)]=$+setminstats *=*:@break not(u(isadmin,%#)); @break findplayer(%0); @break case(1,[strmatch(%1,clear)],pemit(%#,ansi(h,Minstats cleared for [name(%qp)].))[wipe(%qp/stats`min*)]1,[regmatch(%1,^(%\d+) (%\d+) (%\d+) (%\d+) (%\d+)$,-1 0 1 2 3 4)],0,pemit(%#,ansi(r,Invalid arguments. Provide 5 numbers separated by spaces.))1); @pemit %#=ansi(h,Set minstats for [name(%qp)]. Skill: [ansi(hx,%q0)] Strength: [ansi(hx,%q1)] Evasion: [ansi(hx,%q2)] Endurance: [ansi(hx,%q3)] Aid: [ansi(hx,%q4)]); think [set(%qp,stats`minskill:%q0)][set(%qp,stats`minstrength:%q1)][set(%qp,stats`minevasion:%q2)][set(%qp,stats`minendurance:%q3)][set(%qp,stats`minaid:%q4)]

&CMD`+STAFFTITLE [u(cobj,staff_globals)]=$+STAFFTITLE *:@force/inline %#=+channel/stafftag %0

&CMD`THEMEPLS [u(cobj,staff_globals)]=$+themepls:@pemit %#=u(header,PLs by Theme);@dolist u(setr,themes,sortkey(#lambda/get(u(tdb)/\%0),reglattr(u(tdb)/^\\d+$),i))={@pemit %#=get(u(tdb)/%i0): [iter(get(u(tdb)/%i0`CAST),default(%i0/level,??))];@switch/inline eq(inum(0),words(%q<themes>))=1,{@pemit %#=u(subheader)}}

&CMD`THEMEPLS-ARG [u(cobj,staff_globals)]=$+themepls *:@pemit %#=u(header,PLs for Themes: %0);@dolist u(setr,themes,sortkey(#lambda/get(u(tdb)/\%0),grepi(u(tdb),*,%0),i))={@pemit %#=get(u(tdb)/%i0): [iter(get(u(tdb)/%i0`CAST),default(%i0/level,??))];@switch/inline eq(inum(0),words(%q<themes>))=1,{@pemit %#=u(subheader)}}

&U_IDLEL [u(cobj,staff_globals)]=filter(#lambda/gt(sub(secs(),convtime(u(\%0/LASTLOGOUT))) ,mul(2592000,%0)),lsearch(all,eplayer,\[not(u(##/idleignore))\]))
@set [u(cobj,staff_globals)]/U_IDLEL=no_command
&WA [u(cobj,staff_globals)]=#1263

&CMD`GUESTBAN [u(cobj,staff_globals)]=$+guestban *:@switch [orlflags(%#,royalty wizard)]=1,{@switch [haspower(*%0,guest)]=1,{think [u(setq,0,[get(*%0/lastsite)])];@boot *%0;@sitelock %q0=!guest;@pemit %#=Site banned: %q0;@mail Goddish=Guest ban used/Guest ban used against %0 - site %q0},{@pemit %#=That is not a guest.}},{@pemit %#=You are not authorized to use that command.}