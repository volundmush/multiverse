@@ DremCombatCode V2.1 (#2828)
@create DremCombatCode V2.1
@link #2828 = #1338
@lock/Use #2828=LOCK-USE/0
@lset #2828/Use=no_inherit
@set #2828 = WIZARD
&ATT_FUNCTIONS #2828=#2684
&ATT_SIGFUNCTIONS #2828=#1618
&CMD_1HPLEFT #2828=$+1hpleft *:[setq(t,%0)][u([u(ATT_FUNCTIONS)]/fn_cmd.attack.va)]; @break %qz; @remit %l=[ansi(hx,[name(%#)])] attacks with [ansi(x,**** ****** *** ****)]. [name([pmatch(%0)])] is reduced to [ansi(hr,1)] HP.;@set %qt=HP_NOW:1
&CMD_ADMINHEAL #2828=$+adminheal *=*:think [setq(t,[pmatch(%0)])][switch([orflags(%#,Wr)][isint(%1)][type(%qt)],0*,[pemit(%#,We're sorry\, but you do not have access to this command. Please kindly commit seppuku and have a nice night.)],10*,[pemit(%#,We're sorry\, but you can only raise or lower someone's HP by a numerical amount. Non-numerical values such as CAT or LOVE are not supported by this code.)],11PLAYER,[set(%qt,HP_NOW:[add([u(%qt/hp_now)],%1)])][pemit(%#,You [if(gt(%1,0),raise,lower)] [name(%qt)]'s HP by [abs(%1)] points. This leaves them at [u(%qt/hp_now)] points.)],[pemit(%#,We're sorry\, but %0 is not a player. Please check your name and number and try your HP command again.)])]
&CMD_+ADMINSTRIKE #2828=$^\+adminstrike ([^=]*)\=([0-7s])(\:(.*))?$:@pemit %#=[setq(a,%2)][setq(t,[pmatch(%1)])][setq(j,%4)]; @break [not(orflags(%#,Wr))]; @remit/list [if(strmatch(%l,[loc(%qt)]),%l,%l [loc(%qt)])]=[setq(c,[u([u(ATT_FUNCTIONS)]/fn_mathattackeval)])][u([u(ATT_FUNCTIONS)]/fn_ATADJ)][u([u(ATT_FUNCTIONS)]/fn_hitstatuscheck)][if([or([not(isint(%qa))],eq(%qa,0))],setq(c,[add(%qc,-12)]))][switch([lt(%qc,-6)][gte(%qc,-6)][gte(%qc,-2)][gte(%qc,4)][gte(%qc,7)][gte(%qc,15)][gte(%qc,31)][gte(%qc,39)],10*,[u([u(ATT_FUNCTIONS)]/FN_ON.MISS2)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],8)])],010*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,10)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],6)])],0110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,25)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],3)])],01110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,50)],011110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,75)],0111110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,100)][set(%#,combat`missstr:0)],01111110,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,115)][set(%#,combat`missstr:-2)],01111111,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,130)][set(%#,combat`missstr:-4)])][u([u(ATT_FUNCTIONS)]/fn_durcheck)]
@set #2828/CMD_+ADMINSTRIKE=regexp
&CMD_AIDSET #2828=$+AIDMAX *=*:think [switch([orflags(%#,Wr)][hastype(pmatch(%0),Player)][and([isint(%1)],[gte(%1,0)])],0*,pemit(%#,I'm sorry. Only admin can use this command.),10*,pemit(%#,I'm sorry. You can only use this command on players.),110,pemit(%#,Please use an integer value. Preferably positive.),111,[pemit(%#,You set %0's maximum aid to %1.)][set([pmatch(%0)],combat`AIDAPPROVE:%1)],[pemit(%#,BAD STAFFER: Make sure you're trying to set things on people that exist.)])]
&CMD_+ALLOW #2828=$+allow: &status %#= replace(get(%#/status),1,1,|) ; @pemit %#=You have +allowed. The next attack made against you will hit.
&CMD_ALLOWKOSPEC #2828=$+allowko *:think [setq(t,[pmatch(%0)])][switch([not(match([u(%#/combat`legacy`tdata`KOLIST)],*%qt*))][orflags(%qt,P)],00,[pemit(%#,%0 is not a player. Please try again.)],01,[pemit(%#,[name(%qt)] will be removed from your list for instant KOs.)][set(%#,combat`legacy`tdata`KOLIST:[setdiff([u(%#/combat`legacy`tdata`KOLIST)],%qt,|)])],11,[pemit(%#,[ansi(hr,##WARNING!##)] [ansi(h,The next time you are hit by [name(%qt)] you will be knocked out. No matter what.)])][set(%#,combat`legacy`tdata`KOLIST:[setunion([u(%#/combat`legacy`tdata`KOLIST)],%qt,|)])])]
&CMD_ALLOWSPEC #2828=$+allow *:think [setq(t,[pmatch(%0)])][switch([not(match([u(%#/combat`allowlist)],*%qt*))][orflags(%qt,P)],00,[pemit(%#,%0 is not a player. Please try again.)],01,[pemit(%#,[name(%qt)] will be removed from your allow list.)][set(%#,combat`allowlist:[setdiff([u(%#/combat`allowlist)],%qt,|)])],11,[pemit(%#,[name(%qt)] has been added to your allow list.)][set(%#,combat`allowlist:[setunion([u(%#/combat`allowlist)],%qt,|)])])]
&CMD_ALLVACATION #2828=$+alighteveryoneoutofthepool:think [if([hasflag(%#,Wizard)],[iter([u(#546/adminlist)],[set([before(##,:)],duty:VACATION)],%b,%b)] [pemit(%#,You have set all admin on vacation.)],[pemit(%#,You cannot use this command.)])]
&CMD_ATTBUFF #2828=$+Attbuff *:think [setq(a,[last(u(%#/stats),|)])][switch([strmatch(loc(%#),loc(*%0))][gt(%qa,2)],0*,[pemit(%#,You are not in the same room as your target.)],10,pemit(%#,You must have an AID stat of 2 or more to use this command.),11,[remit(loc(%#),[ansi(hy,[name(%#)] uses an epic offensive buff on [name(num(*%0))].)])][set(*%0,combat`ubuff-att:1)])]
&CMD_ATTRHIDE1 #2828=$+attributehide:think switch([orflags(%#,WrJ)][default(%#/adminattrs`noscan,0)],0*,[pemit(%#,You must be a GM%, Admin or a Wizard to use this command.)],10,[pemit(%#,You set your stats to be TP Visible. You will be able to see your own stats as will admin. Other players will not be able to see them.)][set(%#,adminattrs`noscan:1)],11,[pemit(%#,Your stats are no longer TP Visible only.)][set(%#,adminattrs`noscan:0)])
&CMD_ATTRHIDE2 #2828=$+attributehide *:think switch([orflags(%#,Wr)][default(*%0/adminattrs`noscan,0)],0*,[pemit(%#,You must be an Admin or a Wizard to use this command.)],10,[pemit(%#,You set [name(*%0)] stats to be TP Visible. They will be able to see their own stats as will admin. Other players will not be able to see them.)][set(*%0,adminattrs`noscan:1)][pemit(*%0,[name(%#)] has set your stats to be TP Visible only. Admin will be able to see your stats%, and you will be able to see your stats. Other players will not be able to.)],11,[pemit(%#,You set it to where [name(*%0)]'s stats are no longer TP visible.)][pemit(*%0,[name(%#)] has fixed it to where your stats are no longer TP Visible only.)][set(*%0,adminattrs`noscan:0)])
&CMD_AUTHORITYBUFF #2828=$+authoritybuff *=*:think switch([isint(%1)][orflags(*%0,P)][orflags(%#,Wr)][orflags(%#,J)][default(%#/adminattrs`isfachead,0)],0*,pemit(%#,You must debuff/buff someone with an integer number.),10*,pemit(%#,You need to use authority mode on a player.),11001,[pemit(%#,Please use the command +authoritymode or contact an admin if you need something other then the authority buff.)],11010,pemit(%#,You have to be a Senior GM to be able to use this power. Please contact a Senior GM.),11011,[setq(b,[if(gt(%1,25),25,%1)])] pemit(%#,You have [ifelse(gt(%qb,0),authority buffed [name(*%0)] by %qb percent.,ifelse(lt(%qb,0),authority debuffed [name(*%0)] by %qb percent.,removed any authority buff on [name(*%0)].))] This will be noted as 'authority' if buffed or 'exhaustion' if debuffed at the end of their attacks.) [set(*%0,combat`authority:[mul(%qb,0.01)])][remit(loc(*%0),[name(*%0)] is [ifelse([gt(%qb,0)],now authority buffed.,[ifelse(lt(%qb,0),now exhausted.,no longer authority buffed.)])])],111*,[setq(b,%1)][pemit(%#,You have [ifelse(gt(%qb,0),authority buffed [name(*%0)] by %qb percent.,ifelse(lt(%qb,0),authority debuffed [name(*%0)] by %qb percent.,removed any authority buff on [name(*%0)].))] This will be noted by the word 'authority' if buffed or 'exhaustion' if debuffed at the end of their attacks.)[set(*%0,combat`authority:[div(%qb,100)])][remit(loc(*%0),[name(*%0)] is [ifelse([gt(%qb,0)],now authority buffed.,[ifelse(lt(%qb,0),now exhausted.,no longer authority buffed.)])])][set(*%0,combat`authority:[mul(%qb,0.01)])],11000,pemit(%#,You are not permitted to use this command.))
&CMD_AUTHORITYMODE #2828=$+authoritymode:think switch([gt(default(%#/combat`authority,0),0)][default(%#/adminattrs`isfachead,0)],00,pemit(%#,You must have fachead access to use this.),01,[pemit(%#,You are now in authority mode. You have the eqiv of a [switch([u(%#/rank)],8,25,7,21,6,10,1)] percent buff.)][set(%#,combat`authority:0.[switch([u(%#/rank)],8,25,7,21,6,10,1)])][remit(loc(%#),[name(%#)] is now in authority mode. Beware. They will kick your ass.)],1*,[pemit(%#,You are no longer in authority mode.)][set(%#,combat`authority:0)][remit([loc(%#)],[name(%#)] is no longer in authority mode.)])
&CMD_BOSSMODEREMOVE #2828=$+removeboss *=*:think [switch([orflags(%#,W)][orflags([pmatch(%0)],P)][t(default([pmatch(%0)]/combat`legacy`bossmode-[edit(%1,%b,_)],0))],0*,pemit(%#,You are not a wizard. Go away.),10*,pemit(%#,You have to use this command on a person.),110,[pemit(%#,[name(pmatch(%0))] does not have the boss mode %1.)],111,pemit(%#,You remove the bossmode %1 from [name(pmatch(%0))].)[wipe([pmatch(%0)]/combat`legacy`bossmode-[edit(%1,%b,_)])])]
&CMD_BOSSOFFBITCH #2828=$+BOSS:think [switch([default(%#/combat`legacy`bosson,0)],0,[pemit(%#,Boss mode is already off.)],1,[pemit(%#,You turn off your boss mode.)][set(%#,combat`legacy`bosson:0)][remit(%l,[ansi(hr,%n turns off %p boss mode.)])])]
&CMD_BOSSONBITCH #2828=$+BOSS *:think [switch([default(%#/combat`legacy`bosson,0)][t(default(%#/combat`legacy`bossmode-[edit(%0,%b,_)],0))],00,[pemit(%#,You don't have that boss mode.)],01,[set(%#,combat`legacy`bossatk:[before([u(%#/combat`legacy`bossmode-[edit(%0,%b,_)])],|)])][set(%#,combat`legacy`bossdef:[after([u(%#/combat`legacy`bossmode-[edit(%0,%b,_)])],|)])][pemit(%#,You turn on the boss mode : %0 : You now have a [mul(100,[default(%#/combat`legacy`bossatk,0)])] percent bonus to attack%, and a [mul(100,[default(%#/combat`legacy`bossdef,0)])] percent bonus to defense.)][set(%#,combat`legacy`bosson:1)][remit(%l,[ansi(hr,%n turns on the boss mode: [ansi(hr,lcstr(%0))]. %P attacks will be appended by the word : <BOSS>. %P base attack is [if(gt([before([u(%#/combat`legacy`bossmode-[edit(%0,%b,_)])],|)],0),increased,[if(lt([before([u(%#/combat`legacy`bossmode-[edit(%0,%b,_)])],|)],0),decreased,the same)])]. %P defense is [if(gt([after([u(%#/combat`legacy`bossmode-[edit(%0,%b,_)])],|)],0),increased,[if(lt([after([u(%#/combat`legacy`bossmode-[edit(%0,%b,_)])],|)],0),decreased,the same)])].)])],11,[set(%#,combat`legacy`bossatk:[before([u(%#/combat`legacy`bossmode-[edit(%0,%b,_)])],|)])][set(%#,combat`legacy`bossdef:[after([u(%#/combat`legacy`bossmode-[edit(%0,%b,_)])],|)])][pemit(%#,You switch to boss mode : %0 : You now have a [mul(100,[default(%#/combat`legacy`bossatk,0)])] percent bonus to attack%, and a [mul(100,[default(%#/combat`legacy`bossdef,0)])] percent bonus to defense.)][set(%#,combat`legacy`bosson:1)][remit(%l,[ansi(hr,%n switches boss modes to: [ansi(hr,lcstr(%0))]. %P attacks will be appended by the word : <BOSS>. %P base attack is [if(gt([before([u(%#/combat`legacy`bossmode-[edit(%0,%b,_)])],|)],0),increased,[if(lt([before([u(%#/combat`legacy`bossmode-[edit(%0,%b,_)])],|)],0),decreased,the same)])]. %P base defense is [if(gt([after([u(%#/combat`legacy`bossmode-[edit(%0,%b,_)])],|)],0),increased,[if(lt([after([u(%#/combat`legacy`bossmode-[edit(%0,%b,_)])],|)],0),decreased,the same)])].)])])]
&CMD_BUFF #2828=$+Buff *:@assert strmatch(loc(setr(target,num(*%0))),%l)=@pemit %#=You are not in the same room as [name(%q<target>)].;@assert gte(setr(a,last(u(%#/stats),|)),2)=@pemit %#=You must have an AID stat of 2 or more to use this command.;th u(u(ATT_FUNCTIONS)/fn_durcheck);@remit %l=ansi(hg,%n buffs [name(%q<target>)].);&combat`deandbuffdur %q<target>=if(setr(0,default(%q<target>/combat`deandbuffdur,0)),add(%q0,div(add(div(%qa,2),4,if(strmatch(%#,%q<target>),3,0)),2)),add(div(%qa,2),4,if(strmatch(%#,%q<target>),3,0)));&combat`BuffPer %q<target>=add(mul(%qa,0.0125),0.06,if(strmatch(%#,%q<target>),0.04,0))
&CMD_+CATTACK #2828=$^\+combo ([^=]*)\=([1-5])(\:(.*))?$:@pemit %#=[setq(a,%2)][setq(t,%1)][setq(j,%4)][u([u(ATT_FUNCTIONS)]/fn_cmd.attack.va)][set(%qt,COMBAT`LEGACY`TDATA`HP_WAS:[u(%qt/hp_now)])]; @break %qz; @remit %l=[ansi(h,%n uses [if(%qj,%qj%b)][u([u(ATT_FUNCTIONS)]/fn_att.type)] combo attack on [name(%qt)].)]%r[ansi(y,The attacks resulted in a series of\, [iter(lnum([add(%qa,1)]),[if(eq(##,%qa),and%b)][u([u(att_functions)]/fn_comboroll)][if(neq(##,%qa),%,)],%b,%b)] strikes for a total of [sub([u(%qt/COMBAT`LEGACY`TDATA`HP_WAS)],[u(%qt/HP_NOW)])] damage.)] [if(lt([u(%#/combat`authority)],0),[ansi(hy,%B<EXHAUSTION>)])][if(gt([u(%#/combat`authority)],0),[ansi(hy,%B<AUTHORITY>)])][if([u(%#/COMBAT`LEGACY`BOSSON)],%b[ansi(hr,<BOSS>)])][u([u(ATT_FUNCTIONS)]/fn_durcheck)][pemit(%qt,[ansi(h,%n used a combo attack on you and hit you for a total of [sub([u(%qt/COMBAT`LEGACY`TDATA`HP_WAS)],[u(%qt/HP_NOW)])] damage.)])][u([u(att_functions)]/fn_ko)][if([getsta(%qt,ALLOW)],[set(%qt,Status:[replace(get(%qt/status),1,0,|)])])]
@set #2828/CMD_+CATTACK=regexp
&CMD_CHECKPL #2828=$+checkpl:@assert isadmin(%#);think setq(l,search(all,type,player,elock,faction:unaffiliated|faction:union|faction:confederate,eplayer,%[gt(get(##/level),36)%])); think [setq(p,setunion(iter(%ql,get(%i0/level)),))][setq(l,sort(iter(%ql,[get(%i0/level)]:%i0)))]; @pemit/silent %#=repeat([center(< NAME >,22,-)]|PL|,3);@pemit/silent %#=iter(setinter(%qp,37 38) [if(setdiff(%qp, 37 38),#-1)],[table(iter(firstof(graball(%ql,%i0:*),regraball(%ql,^%(?:39|%[^3%]%))),%b[ljust(name(after(%i0,:)),21)]|[before(%i0,:)]|,,;),26,78,;,)],,%r);@pemit/silent %#=repeat(=,78)
&CMD_+DATTACK #2828=$^\+dattack ([^=]*)\=([0-5s])(\:(.*))?$:@pemit %#=[setq(a,%2)][setq(t,%1)][setq(j,%4)][u([u(ATT_FUNCTIONS)]/fn_cmd.attack.va)]; @break %qz; @remit %l=[ansi(hr,<DRAMATIC ATTACK>)]%b[setq(c,[u([u(ATT_FUNCTIONS)]/fn_mathattackeval)])][if([or([not(isint(%qa))],eq(%qa,0))],setq(c,[add(%qc,-14)]))][setq(c,[add(%qc,15)])][u([u(ATT_FUNCTIONS)]/fn_ATADJ)][if(lt([u(%#/level)],25),setq(c,add(%qc,10)))][u([u(ATT_FUNCTIONS)]/fn_hitstatuscheck)][switch([lt(%qc,-6)][gte(%qc,-6)][gte(%qc,-2)][gte(%qc,4)][gte(%qc,7)][gte(%qc,15)][gte(%qc,31)][gte(%qc,42)],10*,[u([u(ATT_FUNCTIONS)]/FN_ON.MISS2)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],8)])],010*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,10)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],6)])],0110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,25)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],3)])],01110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,50)],011110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,75)],0111110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,100)],01111110,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,115)],01111111,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,130)])][u([u(ATT_FUNCTIONS)]/fn_durcheck)]
@set #2828/CMD_+DATTACK=regexp
&CMD_DEBUFF #2828=$+deBuff *:think [setq(a,[last(u(%#/stats),|)])][switch([strmatch(loc(%#),loc(*%0))][gt(%qa,1)],0*,[pemit(%#,You are not in the same room as your target.)],10,pemit(%#,You must have an AID stat of 2 or more to use this command.),11,[remit(loc(%#),[u([u(ATT_FUNCTIONS)]/fn_durcheck)][ansi(g,[name(%#)] debuffs [name(num(*%0))].)])][setq(0,[default(*%0/combat`deandbuffdur,0)])][switch([lt(%q0,0)],1,[set(*%0,combat`deandbuffdur:[add([mul(%qa,-1)],-2)])],0,[set(*%0,combat`deandbuffdur:[sub(%q0,[add(%qa,1)])])])][set(*%0,COMBAT`deBuffPer:[mul(%qa,[u([u(ATT_FUNCTIONS)]/Attrib-deBUFFPERAID)])])])]
&CMD_DEFBUFF #2828=$+Defbuff *:think [setq(a,[last(u(%#/stats),|)])][switch([strmatch(loc(%#),loc(*%0))][gt(%qa,3)],0*,[pemit(%#,You are not in the same room as your target.)],10,pemit(%#,You must have an AID stat of 4 or more to use this command.),11,[remit(loc(%#),[ansi(y,[name(%#)] uses an epic defensive buff on [name(num(*%0))].)])][set(*%0,combat`ubuff-def:1)])]
&CMD_EXECUTESIGMOVE #2828=$+sigmove *=*:think [setq(h,0)][setq(t,[pmatch(%0)])][setq(m,[ucstr(combat`legacy`sgmove-[edit(%1,%b,_)])])][setq(i,[u(%#/%qm)])][switch([strmatch([loc(%#)],[loc(%qt)])][hasattr(%#,%qm)][gt(u(%qt/hp_now),0)][gt(u(%#/hp_now),0)],0*,[pemit(%#,The target is not in the same room as you.)],10*,[pemit(%#,You do not have that signature move.)],110*,[pemit(%#,[name(%qt)] is already knocked out.)],1110,[pemit(%#,You are knocked out.)],1111,[pemit(%#,You use a signature move!)][u([u(ATT_FUNCTIONS)]/fn_sigsetup)][remit(%l,[ansi(hg,%n uses the [if([before(after(%qi,MULTITARGET:),|)],multi,single)]-target signature move **%qj**.)])][setq(a,[before(after(%qi,LEVEL:),|)])][setq(c,[u([u(ATT_FUNCTIONS)]/fn_mathattackevalsig)])][u([u(ATT_FUNCTIONS)]/fn_ATADJ)][u([u(ATT_FUNCTIONS)]/fn_hitstatuschecksig)][if([or([not(isint(%qa))],eq(%qa,0))],setq(c,[add(%qc,-14)]))][if([gt(%qc,-6)],setq(h,1),setq(h,0))][remit(%l,[switch([lt(%qc,-6)][gte(%qc,-6)][gte(%qc,-2)][gte(%qc,4)][gte(%qc,7)][gte(%qc,15)][gte(%qc,31)][gte(%qc,42)],10*,[u([u(ATT_FUNCTIONS)]/FN_ON.MISSSIG)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],8)])],010*,[u([u(ATT_FUNCTIONS)]/FN_ON.HITSIG,10)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],6)])],0110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HITSIG,25)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],3)])],01110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HITSIG,50)],011110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HITSIG,75)],0111110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HITSIG,100)][set(%#,combat`missstr:0)],01111110,[u([u(ATT_FUNCTIONS)]/FN_ON.HITSIG,115)][set(%#,combat`missstr:-2)],01111111,[u([u(ATT_FUNCTIONS)]/FN_ON.HITSIG,130)][set(%#,combat`missstr:-4)])])][u([u(ATT_FUNCTIONS)]/fn_sigwrapup)],[pemit(%#,We're not sure how you got this : But it's bad that you did. Contact Dremarian and let him know what you were doing.)])]
&CMD_EXPENDBUFF #2828=$+expendturn:think [remit(%l,[ansi(hc,[name(%#)] expends one turn. This effects buffs%, sigmoves%, and extended damage duration. It should be used only if you did not attack normally but spend a round action doing something.)])] [setq(x,default(%#/COMBAT`DEANDBUFFDUR,0))] [u([u(ATT_FUNCTIONS)]/fn_falloffsig)][u([u(ATT_FUNCTIONS)]/fn_falloffbuff)][u([u(ATT_FUNCTIONS)]/fn_X_overtime)])]
&CMD_FIX #2828=$+fix *=*:@switch [nearby(%#,[setr(z,pmatch(%0))])]=1,{@switch [cand(isint(%1),lte(%1,3),gte(%1,1))]=1,{@switch [gt(setr(0,add(get(%qz/hp_now),setr(1,ulocal([u(ATT_FUNCTIONS)]/u_aid_calc_2,getnum(%#,aid),%1)))),setr(2,get(%qz/hp_max)))]=1,{&hp_now %qz=%q2;@remit [loc(%#)]=[ansi(hc,%N [u([u(ATT_FUNCTIONS)]/fn_durcheck)]fully restored [fullname(%qz)] (Level %1). ***)]},{&hp_now %qz=%q0;@remit [loc(%#)]=[ansi(hc,%N [u([u(ATT_FUNCTIONS)]/fn_durcheck)]restored [fullname(%qz)] by %q1 (Level %1).)]}},{@pemit %#=The level specified is invalid.  Please enter a level between 1 and 3.}},{@pemit %#=That target is not in the room.}
&CMD_+GATTACK #2828=$^\+attack ([^=]*)\=([0-5s])(\:(.*))?$:@pemit %#=[setq(a,%2)][setq(t,%1)][setq(j,%4)][u([u(ATT_FUNCTIONS)]/fn_cmd.attack.va)]; @break %qz; @remit %l=[setq(c,[u([u(ATT_FUNCTIONS)]/fn_mathattackeval)])][u([u(ATT_FUNCTIONS)]/fn_ATADJ)][u([u(ATT_FUNCTIONS)]/fn_hitstatuscheck)][if([or([not(isint(%qa))],eq(%qa,0))],setq(c,[add(%qc,-12)]))][switch([lt(%qc,-6)][gte(%qc,-6)][gte(%qc,-2)][gte(%qc,4)][gte(%qc,7)][gte(%qc,15)][gte(%qc,31)][gte(%qc,39)],10*,[u([u(ATT_FUNCTIONS)]/FN_ON.MISS2)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],10)])],010*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,10)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],8)])],0110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,25)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],4)])],01110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,50)],011110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,75)],0111110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,100)][set(%#,combat`missstr:0)],01111110,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,115)][set(%#,combat`missstr:-2)],01111111,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,130)][set(%#,combat`missstr:-4)])][u([u(ATT_FUNCTIONS)]/fn_durcheck)]
@set #2828/CMD_+GATTACK=regexp
&CMD_GETSIGINFO #2828=$+getsiginfo */*:@break findplayer(%0); think [setq(t,%qp)][setq(n,strcat(combat`legacy`sgmove-,[edit(%1,%b,_)]))][setq(8,[u([u(ATT_FUNCTIONS)]/fn_getran,SELFDAMAGE:)])][switch([orflags(%qp,P)][hasattr(%qp,%qn)],0*,[pemit(%#,[ansi(hr,ERROR!)] %0 is not a player.)],10,[pemit(%#,[name(%qp)] does not have a signature move of %1.)],11,[pemit(%#,[name(%qp)]'s signature move of %1:%r[center(:PRIMARY ATTACK VALUES:,60)]%rSKILL :  [u([u(ATT_FUNCTIONS)]/fn_getran,SKILL:)] | STRENGTH : [u([u(ATT_FUNCTIONS)]/fn_getran,STRENGTH:)] | Level :  [u([u(ATT_FUNCTIONS)]/fn_getran,LEVEL:)] | Damage Mod : [u([u(ATT_FUNCTIONS)]/fn_getran,ATTDAMAGE:)] | Attackbuff :  [u([u(ATT_FUNCTIONS)]/fn_getran,PREBUFF:)]%r[center(:POST ATTACK VALUES:,60)]%rOffense buff :  [u([u(ATT_FUNCTIONS)]/fn_getran,POSTBUFFATT:)] | Defense buff :  [u([u(ATT_FUNCTIONS)]/fn_getran,POSTBUFFDEF:)] | Buff Falloff :  [u([u(ATT_FUNCTIONS)]/fn_getran,POSTBUFFFALL:)] %RSelf [if([not(strmatch([before(after([u(%qp/%qn)],SELFDAMAGE:),|)],-*))],Damage : %q8,Heal : [if(lt(%q8,0),[mul(%q8,-1)],%q8)])] | Self stun : [if([before(after([u(%qp/%qn)],STUN:),|)],Yes,No)] | Multi-Target : [if([before(after([u(%qp/%qn)],MULTITARGET:),|)],Yes,No)]%rSecondary information : [before(after([u(%qp/%qn)],INFO:),|)])])])]
&CMD_GETSIGNAMES #2828=$+getsigmoves *:@break findplayer(%0); think pemit(%#,[name(%qp)] has the following signature moves:%r[Iter(lattr(%qp/combat`legacy`sgmove-**),[center(edit([after(%i0,SGMOVE-)],_,%b),35)],,%r)])
&CMD_+GMHURT #2828=$^\+gmhurt ([0-9]?[0-9]?[0-9]?)$:@pemit %#=[ansi(ch,You have silently lowered your current HP [%1] points, from [setr(1,u([u(ATT_FUNCTIONS)]/uhp_now))] to [sub(%q1,%1)].)] ; &HP_NOW %#=[sub(%q1,%1)]
@set #2828/CMD_+GMHURT=regexp
&CMD_+GMPULL #2828=$+gmpull *:think [switch([orflags(%#,WrJ)][u([u(ATT_FUNCTIONS)]/hp_isgoodgmpull,%0)][isnum(%0)],0**,[pemit(%#,You must be a GM or Staff to use this command.)],10*,[pemit(%#,You must set your HP between 1 and 600.)],110,[pemit(%#,Try setting an actual NUMBER for your HP.)],111,[remit(loc(%#),[ansi(hg,[name(%#)] has set their MAX HP from [u(%#/HP_max)] to [trunc(%0)]. They were at [u(%#/HP_now)].)])][set(%#,HP_NOW:[trunc(%0)])][set(%#,HP_MAX:[trunc(%0)])])]
&CMD_+HEALME #2828=$^\+healme (\d+)$:th setq(1,u(u(ATT_FUNCTIONS)/uhp_now));@remit %l=ansi(ch,%n has raised %p current HP [%1] points\, from [if(get(%#/ADMINATTRS`NOSCAN),???,%q1)] to [if(get(%#/ADMINATTRS`NOSCAN),???,add(%q1,%1))].);&HP_NOW %#=add(%q1,%1)
@set #2828/CMD_+HEALME=regexp
&CMD_HELP_SIGHELP #2828=$+sighelp:@pemit %#=[repeat(=,78)]%r%rThe current list of commands for signature moves are :%R+sighelp : Obviously this brings up a help file! (Somehow I think you knew this already.)%r%r+getsigmoves : This checks to see if a person has signature moves.%rFull Syntax +getsigmoves TARGET%r%r+Getsiginfo : This command gets you specific information on someone's signature move.%rFull Syntax : +getsiginfo CHARACTERNAME/MOVENAME%r%r+Sigmove : This uses a signature move on someone. If this move targets either yourself, or no one, you should use it on yourself.%RFull Syntax : +sigmove TARGET=NAMEOFMOVE%r%r[repeat(=,78)]
&CMD_HITMOD_ADMINLOWER #2828=$+attackmod */lower *=*:think [switch([orflags(%#,WrJ)][hastype(%#,Player)][cor([cor([strmatch(LOW,[ucstr(%1)])],[strmatch(MEDIUM,[ucstr(%1)])],[strmatch(HIGH,[ucstr(%1)])])],[and(orflags(%#,Wr),strmatch(EXTREME,[ucstr(%1)]))])],0*,[pemit(%#,You should not be using this command. The admin have been notified.)][cemit(**code alerts**,[ansi(hb,[time()])] [ansi(hgu,COMBAT SYSTEM NOTICE)] : [ansi(hg,[name(%#)])] has attemped to use the hitmod command. They should not have access or know about this command. Find out why.,noisy)],10*,[pemit(%#,%0 is not a valid player name or Alias. Please try again.)],110,[Pemit(%#,For the amount of adjustment : You must use LOW%, MEDIUM%, or HIGH if you are a GM. Admin may use EXTREME.)],111,[pemit(%#,You have adjusted [name(*%0)]'s hit/crit rate by [capstr(%1)]. It has been announced to the admin. This has not been announced to the players in your scene.)][set([pmatch(%0)],combat`missstr:[add(default([pmatch(%0)]/combat`missstr,0),[switch([ucstr(%1)],LOW,-2,MEDIUM,-4,HIGH,-6,EXTREME,-10)])])][cemit(**code alerts**,[ansi(hb,[time()])] [ansi(hgu,COMBAT SYSTEM NOTICE)] : [ansi(hg,[name(%#)])] has used a command to [Ansi(u,LOWER)] [ansi(hg,[name(*%0)]'s)] hit/critical rate by a [ucstr(%1)] amount for the following reason : [ansi(hy,%2)],Noisy)])]
&CMD_HITMOD_ADMINRAISE #2828=$+attackmod */Raise *=*:think [switch([orflags(%#,WrJ)][hastype(%#,Player)][cor([cor([strmatch(LOW,[ucstr(%1)])],[strmatch(MEDIUM,[ucstr(%1)])],[strmatch(HIGH,[ucstr(%1)])])],[and(orflags(%#,Wr),strmatch(EXTREME,[ucstr(%1)]))])],0*,[pemit(%#,You should not be using this command. The admin have been notified.)][cemit(**code alerts**,[ansi(hb,[time()])] [ansi(hgu,COMBAT SYSTEM NOTICE)] : [ansi(hg,[name(%#)])] has attemped to use the hitmod command. They should not have access or know about this command. Find out why.,noisy)],10*,[pemit(%#,%0 is not a valid player name or Alias. Please try again.)],110,[Pemit(%#,For the amount of adjustment : You must use LOW%, MEDIUM%, or HIGH if you are a GM. Admin may use EXTREME.)],111,[pemit(%#,You have adjusted [name(*%0)]'s hit/crit rate by [capstr(%1)]. It has been announced to the admin. This has not been announced to the players in your scene.)][set([pmatch(%0)],combat`missstr:[add(default([pmatch(%0)]/combat`missstr,0),[switch([ucstr(%1)],LOW,2,MEDIUM,4,HIGH,6,EXTREME,10)])])][cemit(**code alerts**,[ansi(hb,[time()])] [ansi(hgu,COMBAT SYSTEM NOTICE)] : [ansi(hg,[name(%#)])] has used a command to [Ansi(u,RAISE)] [ansi(hg,[name(*%0)]'s)] hit/critical rate by a [ucstr(%1)] amount for the following reason : [ansi(hy,%2)],Noisy)])]
&CMD_+HURTME #2828=$^\+hurtme (\d+)$:th setq(1,u(u(ATT_FUNCTIONS)/uhp_now));@remit %l=ansi(ch,%n has lowered %p current HP [%1] points\, from [if(get(%#/ADMINATTRS`NOSCAN),???,%q1)] to [if(get(%#/ADMINATTRS`NOSCAN),???,sub(%q1,%1))].);&HP_NOW %#=sub(%q1,%1)
@set #2828/CMD_+HURTME=regexp
&CMD_KOMEHIT #2828=$+AllowKO:think [if([u(%#/combat`legacy`tdata`KONOW)],[pemit(%#,[ansi(h,The next hit will no longer knock you out unless you take excessive damage.)])][set(%#,combat`legacy`tdata`KONOW:0)],[pemit(%#,[ansi(hr,##WARNING!##)] [ansi(h,The next time you are hit you will be knocked out. No matter what.)])] [set(%#,combat`legacy`tdata`KONOW:1)])]
&CMD_+LESSATTACK #2828=$^\+lessattack ([^=]*)\=([0-5s])(\:(.*))?$:@pemit %#=[setq(a,%2)][setq(t,%1)][setq(j,%4)][u([u(ATT_FUNCTIONS)]/fn_cmd.attack.va)]; @break %qz; @remit %l=[setq(c,[u([u(ATT_FUNCTIONS)]/fn_mathattackeval)])][u([u(ATT_FUNCTIONS)]/fn_ATADJ)][attrib_set(%#/COMBAT`legacy`TDATA`PBFFA,[add([get(%#/COMBAT`legacy`TDATA`PBFFA)],-0.4)])][if([or([not(isint(%qa))],eq(%qa,0))],setq(c,[add(%qc,-14)]))][setq(c,[add(%qc,-8)])][if(lt([u(%#/level)],25),setq(c,add(%qc,10)))][u([u(ATT_FUNCTIONS)]/fn_hitstatuscheck)][switch([lt(%qc,-6)][gte(%qc,-6)][gte(%qc,-2)][gte(%qc,4)][gte(%qc,8)][gte(%qc,17)][gte(%qc,34)][gte(%qc,42)],10*,[u([u(ATT_FUNCTIONS)]/FN_ON.MISS2)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],8)])],010*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,10)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],6)])],0110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,25)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],3)])],01110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,50)],011110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,75)],0111110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,100)],01111110,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,115)],01111111,[u([u(ATT_FUNCTIONS)]/FN_ON.HIT,130)])][u([u(ATT_FUNCTIONS)]/fn_durcheck)][attrib_set(%#/COMBAT`legacy`TDATA`PBFFA,[add([get(%#/COMBAT`legacy`TDATA`PBFFA)],0.4)])]
@set #2828/CMD_+LESSATTACK=regexp
&CMD_LISTBOSS #2828=$+listboss:think [pemit(%#,You have the following bossmodes:)][setq(l,lattr(%#/combat`legacy`bossmode-*,%b))][pemit(%#,[center(NAME,20,-)]|Atk%%|Def%%|%r[iter(%ql,[center([edit([after(##,-)],_,%b)],20)]|[center(mul(100,before([default(%#/##,0)],|)),4)]|[center(mul(100,after([default(%#/##,0)],|)),4)]|,%b,%r)]%rYou can turn on any boss mode listed by typing +boss <NAME> or turn off any boss mode by typing +boss.)]
&CMD_LISTBOSSTARG #2828=$+listboss *:think [switch([orflags(%#,WrJ)],0,[pemit(%#,Only Wizards%, Royals%, and GMs can use this command.)],1[pemit(%#,[name(pmatch(%0))] has the following bossmodes:)][setq(l,lattr([pmatch(%0)]/combat`legacy`bossmode-*,%b))][pemit(%#,[center(NAME,20,-)]|Atk%%|Def%%|%r[iter(%ql,[center([edit([after(##,-)],_,%b)],20)]|[center(mul(100,before([default([pmatch(%0)]/##,0)],|)),4)]|[center(mul(100,after([default([pmatch(%0)]/##,0)],|)),4)]|,%b,%r)])]
&CMD_+MISS #2828=$+miss: &status %#= replace(get(%#/status),2,0,|) ; @pemit %#=You have used +miss. Your next attack will miss.
&CMD_MISSSPEC #2828=$+miss *:think [setq(t,[pmatch(%0)])][switch([not(match([u(%#/combat`misslist)],*%qt*))][orflags(%qt,P)],00,[pemit(%#,%0 is not a player. Please try again.)],01,[pemit(%#,[name(%qt)] will be removed from your miss list.)][set(%#,combat`misslist:[setdiff([u(%#/combat`misslist)],%qt,|)])],11,[pemit(%#,[name(%qt)] has been added to your miss list.)][set(%#,combat`misslist:[setunion([u(%#/combat`misslist)],%qt,|)])])]
&CMD_+NOKOATTACK #2828=$^\+nokoattack ([^=]*)\=([0-5s])(\:(.*))?$:@pemit %#=[setq(a,%2)][setq(t,%1)][setq(j,%4)][u([u(ATT_FUNCTIONS)]/fn_cmd.attack.va)]; @break %qz; @remit %l=[setq(c,[u([u(ATT_FUNCTIONS)]/fn_mathattackeval)])][u([u(ATT_FUNCTIONS)]/fn_ATADJ)][u([u(ATT_FUNCTIONS)]/fn_hitstatuscheck)][if([or([not(isint(%qa))],eq(%qa,0))],setq(c,[add(%qc,-14)]))][switch([lt(%qc,-6)][gte(%qc,-6)][gte(%qc,-2)][gte(%qc,4)][gte(%qc,7)][gte(%qc,15)][gte(%qc,31)][gte(%qc,39)],10*,[u([u(ATT_FUNCTIONS)]/FN_ON.MISS2)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],8)])],010*,[u([u(ATT_FUNCTIONS)]/FN_ON.HITNOKO,10)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],6)])],0110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HITNOKO,25)][set(%#,combat`missstr:[add([u(%#/combat`missstr)],3)])],01110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HITNOKO,50)],011110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HITNOKO,75)],0111110*,[u([u(ATT_FUNCTIONS)]/FN_ON.HITNOKO,100)][set(%#,combat`missstr:0)],01111110,[u([u(ATT_FUNCTIONS)]/FN_ON.HITNOKO,115)][set(%#,combat`missstr:-2)],01111111,[u([u(ATT_FUNCTIONS)]/FN_ON.HITNOKO,130)][set(%#,combat`missstr:-4)])][u([u(ATT_FUNCTIONS)]/fn_durcheck)][if([and([gte(%qc,-6)],[lte([get(%qt/hp_now)],1)])],[set(%qt,status:[replace(get(%qt/status),3,1,|)])][set(%qt,hp_now:1)])]
@set #2828/CMD_+NOKOATTACK=regexp
&CMD_+NOMISS #2828=$+nomiss: &status %#= replace(get(%#/status),2,1,|) ; @pemit %#=You have used +nomiss. Your next attack will be resolved normaly.
&CMD_ODDS #2828=$+odds:th iter(lvplayers(%l),iter(strfirstof(first(getproperty(%i0,FACTIONDB)),UNAFF),setq(%i0,setunion(r(%i0),%i1))));th setq(facs,filterbool(#lambda/listq(),factions() UNAFF));@nspemit %#=There are [itemize(iter(%q<facs>,words(r(%i0)) [if(isdbref(%i0),name(%i0),Unaffiliated)],%b,|),|,and,\,)] characters here.
&CMD_+PULL #2828=$+pull *:think [switch([u([u(ATT_FUNCTIONS)]/hp_isgoodpcpull,%0)][isnum(%0)],0*,[pemit(%#,You must set your HP between 1 and 250.)],10,[pemit(%#,Try setting an actual NUMBER for your HP.)],11,[remit(loc(%#),[ansi(hg,[name(%#)] has set their MAX HP from [u(%#/HP_max)] to [trunc(%0)]. They were at [u(%#/HP_now)].)])][set(%#,HP_NOW:[trunc(%0)])][set(%#,HP_MAX:[trunc(%0)])])]
&CMD_QUICKSCAN #2828=$+roomscan:think [pemit(%#,[center(Name,25,-)][rjust(HP,3)]/[center(Max,3)][rjust(PL,6)] [center(Name,25,-)][rjust(HP,3)]/[center(Max,3)][rjust(PL,6)])][setq(c,0)][setq(p,%# [setdiff(lvplayers(%l),%#)])][pemit(%#,[iter(%qp,[center([ansi(if(isadmin(%i0),hb,getproperty(%i0,factioncolor)),[name(%i0)])],25)][if(default(%i0/ADMINATTRS`NOSCAN,0),???,[rjust(u(%i0/hp_now),3)])]/[if(default(%i0/ADMINATTRS`NOSCAN,0),???,[ljust(u(%i0/hp_max),3)])][if(gt([default(%i0/COMBAT`DEANDBUFFDUR,0)],0),[ansi(hg,*)])][if(lt([default(%i0/COMBAT`DEANDBUFFDUR,0)],0),[ansi(hr,*)])][if(eq([default(%i0/COMBAT`DEANDBUFFDUR,0)],0),%b)][if(default(%i0/ADMINATTRS`NOSCAN,0),??/??,[rjust([lmath(add,get(%i0/stats),|)]/[u(%i0/Level)],5)])][setq(c,[add(%qc,1)])][if([mod(%qc,2)],[space(1)],%r)],,)])]
&CMD_REMOVESIGNATURE #2828=$+removesigmove *=*:think [setq(n,strcat(combat`legacy`sgmove-,[edit(%1,%b,_)]))][switch([orflags(%#,Wr)][orflags([pmatch(%0)],P)][hasattr([pmatch(%0)],%qn)],0*,[pemit(%#,Only Wizards/Admin can use this command. ...Pike off!)],10*,[pemit(%#,So sorry sah, but this has to target a player.)],110,[pemit(%#,[name(pmatch(%0))] don't have that sigmove to remove.)],111,[pemit(%#,You devour the soul of [name(pmatch(%0))]'s signature move '%1'.)][wipe([pmatch(%0)]/%qn)])]
&CMD_+RESET #2828=$+reset:@remit %l=ansi(ch,%n has reset %p combat status to default. %P HP was [get(%#/HP_NOW)]) ; &COMBAT_OK %#=1 ; &HP_MAX %#=[u([u(ATT_FUNCTIONS)]/uhp_max)]; &HP_NOW %#=[u([u(ATT_FUNCTIONS)]/uhp_max)] ; &status %#=0|1|0; &COMBAT`deandbuffdur %#=0; &combat`ubuff-att %#=0; &combat`ubuff-def %#=0; &combat`missstr %#=0;&combat`legacy`bosson %#=0; &COMBAT`LEGACY`TDATA`SGATKBUFF %#=0; &COMBAT`LEGACY`TDATA`SGDEFBUFF %#=0; &COMBAT`LEGACY`TDATA`SGBUFFFALL %#=0; &combat`legacy`tdata`lsd %#=0; &Combat`legacy`tdata`dotlength %#=0; &Combat`legacy`tdata`dotstrength %#=0;&combat`misslist %#=;&combat`allowlist %#=;&combat`legacy`tdata`KONOW %#=0;&combat`legacy`tdata`KOLIST %#=;&combat`mste %#=0;&COMBAT`DEBUFFPER %#=0;&COMBAT`BUFFPER %#=0;&combat`legacy`tdata`pbffa %#=0;&combat`legacy`tdata`pbffd %#=0
&CMD-RESTAT #2828=$+restat *:@assert [setq(g,isgm(%#))][setq(l,default(%#/level,0))][cor(regmatch(%0,get(#2684/regex`restat`player),-1 0 1 2 3 4),cand(%qg,regmatch(%0,get(#2684/regex`restat`gm),-1 0 1 2 3 4)))]=think pemit(%#,ansi(r,Usage: +restat <SKI> <EVA> <STR> <END> <AID>%rMake sure SKL%, EVA%, STR%, and END are between 1 and [if([orflags(WrJ)],15,[default(%qp/combat`statapprove,10)])]. Aid should be between 0 and [default(%#/combat`aidapprove,4)].)); think setq(a,lmath(add, %0)); @assert cor(%qg,lte(%q4,default(%#/combat`aidapprove,4)))=think pemit(%#,ansi(r,Your aid is too high.)); @assert cor(%qg,lte(%qa,%ql))=think pemit(%#,ansi(r,Your stats add up to %qa which exceeds your power level of %ql.)); @assert cor(%qg,setr(e,u(#2684/ufunc-statcheck,%#,%q0|%q1|%q2|%q3|%q4)))=think pemit(%#,ansi(r,switch(%qe,#-1 *,That armor mode is locked.,#-2 *,Your [itemize(setr(t,rest(%qe)),|)] [case(words(%qt,|),1,stat is,stats are)] invalid.%r[iter(ucstr(%qt),%b%b%i0 - [if(gt(setr(v,default(%#/stats`min%i0,0)),0),Minimum: %qv)][if(hasattrval(%#,stats`max%i0),[case(%qv,0,,%b)]Maximum: [default(%#/stats`max%i0,[if([orflags(WrJ)],15,[default(%#/combat`statapprove,10)])])])],|,%r)])));think pemit(%#,You set your stats to a total of %qa out of a maximum of %ql.%rYour stats are set to the following - SKILL: %q0%, EVASION: %q1%, STRENGTH: %q2%, ENDURANCE %q3%, AID: %q4)[attrib_set(%#/stats,%q0|%q1|%q2|%q3|%q4)][oemit(%#,ansi(hr,%N changes %p stats.))]
&CMD_+SCAN_X #2828=$+scan *:think [setr(t,pmatch(%0))][switch([cor(isadmin(%#),strmatch(loc(%#),loc(%qt)))][default(%qt/combat_ok,0)][orflags(%#,WrJ)][default(%qt/adminattrs`noscan,0)],0*,[pemit(%#,You're not in the same room as [ansi(hb,%0)] or [ansi(hb,%0)] is not a player.)],10*,[pemit(%#,[ansi(hb,name(%qt))] is not combat ready.)],1101,[pemit(%#,u([u(ATT_FUNCTIONS)]/fn_scan_dark,%0))],[pemit(%#,u([u(ATT_FUNCTIONS)]/fn_scan_clear,%0))])]
&CMD_SETSIGNATUREMOVE #2828=$+addsigmove *=*-*=*:think [setq(n,[edit(%1,%b,_)])] [iter(0 1 2 3 4 5 6 7 8 9 A,setq(##,[extract(%2,#@,1)],%b,))][switch([orflags(%#,Wr)][orflags(pmatch(%0),P)][and([u([u(ATT_FUNCTIONS)]/fn_strangecheck,%q0)],[u([u(ATT_FUNCTIONS)]/fn_strangecheck,%q1)])][u([u(ATT_FUNCTIONS)]/FN_ATRangecheck,%q2)][u([u(ATT_FUNCTIONS)]/fn_dmgrangecheck,%q3)][and(u([u(ATT_FUNCTIONS)]/fn_buffrangecheck,%q4),[u([u(ATT_FUNCTIONS)]/fn_buffrangecheck,%q5)],[u([u(ATT_FUNCTIONS)]/fn_buffrangecheck,%q6)])][u([u(ATT_FUNCTIONS)]/fn_falloffrangecheck,%q7)][u([u(ATT_FUNCTIONS)]/fn_dmgrangecheck,%q8)][or(eq(%q9,0),eq(%q9,1))][or(eq(%qa,0),eq(%qa,1))],0*,[pemit(%#,We're sorry. But only Royals and Wizzen can use this command.)],10*,[pemit(%#,We're sorry. But this command can only be used on a player.)],110*,[pemit(%#,We're sorry. But Skill and Strength must be an integer between 0 and 15.)],1110*,[pemit(%#,Attack level ranges between 0 and 5.)],11110*,[pemit(%#,Additional damage must range between -26 <healing> and 26 <extra damage>.)],111110*,[pemit(%#,All buff Values must be between -75 <debuff> and 75 <buff>.)],1111110*,[pemit(%#,Buff falloff value must be no greater then 25.)],11111110*,[pemit(%#,Self damage must range between -26 <healing self> and 26 <self damage>.)],111111110*,[pemit(%#,Self stun is either 1 for yes, or 0 for no.)],1111111110,[pemit(%#,Multi-target is 1 for yes, or 0 for no.)],1111111111,[pemit(%#,You setup [name(pmatch(%0))]'s signature move of %1 with the following stats:%r[center(:PRIMARY ATTACK VALUES:,60)]%rSKILL : %Q0 | STRENGTH : %q1 | Level : %q2 |Damage Mod : %Q3 | Attackbuff : %Q4%r[center(:POST ATTACK VALUES:,60)]%rOffense buff : %q5 | Defense buff : %q6 | Buff Falloff : %q7 %RSelf [if(gt(%q8,-1),Damage : %q8,Heal : [mul(-1,%q8)])] | Self stun : [if(%q9,Yes,No)] | Multi-Target : [if(%qa,Yes,No)]%rWith the following information : %3)][set([pmatch(%0)],combat`legacy`sgmove-%qn:[strcat(SKILL:,%q0,|STRENGTH:,%q1,|LEVEL:,%q2,|ATTDAMAGE:,%q3,|PREBUFF:,[mul(%q4,0.01)],|POSTBUFFATT:,[mul(%q5,0.01)],|POSTBUFFDEF:,[mul(%q6,0.01)],|POSTBUFFFALL:,[mul(%q7,0.01)],|SELFDAMAGE:,%q8,|STUN:,%q9,|MULTITARGET:,%qa,|INFO:,%3,|PREEXEC:|POSTEXEC:|)])])]
&CMD_SETUPBOSS #2828=$+addboss *-*=*,*:think [switch([orflags(%#,W)][orflags([pmatch(%0)],P)][and(isnum(%2),isnum(%3))],0*,[pemit(%#,You suck. Stop trying to use this command.)],10*,[pemit(%#,Though you be a wizard : My powers only target players.)],110,[pemit(%#,Wizard though ye be%, Boss bonuses have to be numbers.)],111,[pemit(%#,You set [name(pmatch(%0))] on fire! ...No. Wait. Sorry. Wrong message. Ahem. You setup the boss mode %1 on [name(pmatch(%0))] with a %2 percent offensive bonus%, and a %3 percent bonus to defense.)][set([pmatch(%0)],combat`legacy`bossmode-[edit(%1,%b,_)]:[mul(%2,.01)]|[mul(%3,.01)])])]
&CMD_+SHAKE #2828=$+shake: &status %#= replace(get(%#/status),3,0,|) ; @remit %l=ansi(ch,%N shakes off any stun effects!)
&CMD-STATCHECK #2828=$+statcheck:@assert isadmin(%#); think setq(l,search(all,type,player,elock,stats:*11*|stats:*12*|stats:*5|stats:*6|stats:*7|stats:*8|stats:*9|stats:*10|stats:*11|stats:*12));think pemit(%#,[center(< Statcheck >,78,=)]%r[ljust(Name,20)]%tSk%tEv%tSt%tEn%tAi%tLVL%r[repeat(-,78)]%r[regeditall(edit(iter(%ql,ljust(name(%i0),20)%t[get(%i0/stats)]%t[get(%i0/level)],,%r),|,%t),[get(#2075/regex1-statcheck)],ansi(hr,$0),[get(#2075/regex2-statcheck)],ansi(hr,$0))]%r[repeat(=,78)])
&CMD_STATMAX #2828=$+statapprove *=*:think [if([and(orflags(%#,Wr),[isint(%1)],[gt(%1,0)])],[set([pmatch(%0)],combat`statapprove:%1)][pemit(%#,You have set [name([pmatch(%0)])]'s maximum stats to %1.)],[pemit(%#,You do not have access to this command or have entered a non-integer value.)])]
&CMD_+STATUS #2828=$+stats:@break if(u([u(ATT_FUNCTIONS)]/ucombat_ok),0,pemit(%#,You are not combat ok! Please talk to an admin for help.)1);@pemit %#=center(Combat Status of %N,44,=)%r|[SPACE(7)] HP:%b[ljust(u([u(ATT_FUNCTIONS)]/uhp_now)/[u([u(ATT_FUNCTIONS)]/uhp_max)],7)] [SPACE(6)] Status: [switch(1,lte(u([u(ATT_FUNCTIONS)]/uhp_now),0),KOed,getsta(%#,stun),Stun,Okay)] %b |%r| %b %b Skill: [ljust(getnum(%#,ski),2)] [SPACE(10)] Evasion: [ljust(getnum(%#,eva),2)] %b %b |%r|%b Strength: [ljust(getnum(%#,str),2)] [SPACE(8)] Endurance: [ljust(getnum(%#,end),2)] %b %b |%r| %b %b %b Aid: [ljust(getnum(%#,aid),2)][space(14)]Level: [ljust(u([u(ATT_FUNCTIONS)]/u_lvl,%#),5)] %b|%r| %b %b +miss: [if(getsta(%#,miss),No%b,Yes)] [SPACE(10)] +allow: [if(getsta(%#,allow),Yes,No%b)] %b %b|%r|[center([setq(x,default(%#/COMBAT`DEANDBUFFDUR,0))]Buffed : [if(gt(%qx,0),BUFFED for %qx actions.)][if(lt(%qx,0),DEBUFFED for [mul(-1,%qx)] actions.)][if(eq(%qx,0),No.)],42)]|%r|[center(Epic Buffs : [switch([default(%#/combat`ubuff-att,0)][default(%#/combat`ubuff-def,0)],11,Both,10,Attack,01,Defense,None)],42)]|%r|[center(Sigmoves,42,-)]|%r|[center(Attack [switch([neq([default(%#/combat`legacy`tdata`sgatkbuff,0)],0)][gt([default(%#/combat`legacy`tdata`sgatkbuff,0)],0)],10,Deb,11,B,B)]uff:%b[mul(100,[abs([default(%#/combat`legacy`tdata`sgatkbuff,0)])])]%%,21)][center(Defense [switch([neq([default(%#/combat`legacy`tdata`sgdefbuff,0)],0)][gt([default(%#/combat`legacy`tdata`sgdefbuff,0)],0)],10,Deb,11,B,B)]uff:%b[mul(100,[abs([default(%#/combat`legacy`tdata`sgdefbuff,0)])])]%%,21)]|%r[repeat(=,44)]
&CMD_+STATUS_X #2828=$+stats *:think [setr(t,pmatch(%0))][switch([cor(isadmin(%#),strmatch(loc(%#),loc(%qt)))][default(%qt/combat_ok,0)][orflags(%#,WrJ)][default(%qt/adminattrs`noscan,0)],0*,[pemit(%#,You're not in the same room as [ansi(hb,%0)] or [ansi(hb,%0)] is not a player.)],10*,[pemit(%#,[ansi(hb,name(%qt))] is not combat ready.)],1101,[pemit(%#,u([u(ATT_FUNCTIONS)]/fn_scan_dark,%0))],[pemit(%#,u([u(ATT_FUNCTIONS)]/fn_scan_clear,%0))])]
&CMD_STRIPBUFFS #2828=$+strip:think [pemit(%#,You strip all buffs and debuffs from your character.)][remit([loc(%#)],[ansi(hb,%n strips all buffs and debuffs from their character.)])][set(%#,combat`deandbuffdur:0)][set(%#,combat`ubuff-att:0)][set(%#,combat`ubuff-def:0)][set(%#,COMBAT`LEGACY`TDATA`SGBUFFFALL:0)][set(%#,COMBAT`LEGACY`TDATA`SGDEFBUFF:0)][set(%#,COMBAT`LEGACY`TDATA`SGATKBUFF:0)]
&CMD_+UNALLOW #2828=$+unallow: &status %#= replace(get(%#/status),1,0,|) ; @pemit %#=You have +unallowed. The next attack made against you will be resolved normaly.
&CMD_Y #2828=$YES:think [pemit(%#,You have typed [ansi(hg,Yes)] for yes. If this is correct%, please confirm your response with [ansi(hg,Y)] or [ansi(hg,Yes)].)]
&CMD_YES #2828=$Y:think [pemit(%#,You have typed [ansi(hg,Y)] for yes. If this is correct%, please confirm your response with [ansi(hg,Y)] or [ansi(hg,Yes)].)]
&COMBAT #2828=
@set #2828/COMBAT=no_command wizard mortal_dark
&COMBAT`DEANDBUFFDUR #2828=0
@set #2828/COMBAT`DEANDBUFFDUR=no_command
&COMBAT`LEGACY #2828=
@set #2828/COMBAT`LEGACY=no_command
&COMBAT`LEGACY`BOSSON #2828=0
@set #2828/COMBAT`LEGACY`BOSSON=no_command
&COMBAT`LEGACY`TDATA #2828=
@set #2828/COMBAT`LEGACY`TDATA=no_command
&COMBAT`LEGACY`TDATA`DOTLENGTH #2828=0
@set #2828/COMBAT`LEGACY`TDATA`DOTLENGTH=no_command
&COMBAT`LEGACY`TDATA`DOTSTRENGTH #2828=0
@set #2828/COMBAT`LEGACY`TDATA`DOTSTRENGTH=no_command
&COMBAT`LEGACY`TDATA`LSD #2828=0
@set #2828/COMBAT`LEGACY`TDATA`LSD=no_command
&COMBAT`LEGACY`TDATA`SGATKBUFF #2828=0
@set #2828/COMBAT`LEGACY`TDATA`SGATKBUFF=no_command
&COMBAT`LEGACY`TDATA`SGBUFFFALL #2828=0
@set #2828/COMBAT`LEGACY`TDATA`SGBUFFFALL=no_command
&COMBAT`LEGACY`TDATA`SGDEFBUFF #2828=0
@set #2828/COMBAT`LEGACY`TDATA`SGDEFBUFF=no_command
&COMBAT`MISSSTR #2828=0
@set #2828/COMBAT`MISSSTR=no_command
&COMBAT`UBUFF-ATT #2828=0
@set #2828/COMBAT`UBUFF-ATT=no_command
&COMBAT`UBUFF-DEF #2828=0
@set #2828/COMBAT`UBUFF-DEF=no_command
&COMBAT_OK #2828=1
@set #2828/COMBAT_OK=wizard
&D #2828=
@set #2828/D=wizard mortal_dark
&D`ALTS #2828=0
@set #2828/D`ALTS=no_command
&DESCRIBE #2828=This is current non-unified code object for the combat code.
@set #2828/DESCRIBE=visual
&FN_HPTHADJ #2828=bound(trunc(mul(switch(gte(u(%#/Level),u(%qt/Level)),0,setq(z,mul(0.5,sub(u(%qt/LEVEL),u(%#/Level))))[sub(fdiv(u(%#/HP_NOW),u(%#/HP_MAX)),fdiv(u(%qt/HP_NOW),add(u(%qt/HP_MAX),%qz)))],1,setq(z,mul(.5,sub(u(%#/LEVEL),u(%qt/Level))))[sub(fdiv(u(%#/HP_NOW),add(u(%#/HP_MAX),%qz)),fdiv(u(%qt/HP_NOW),u(%qt/HP_MAX)))]),-10)),-15,15)
&HP_NOW #2828=
@set #2828/HP_NOW=wizard
&LOCK-USE #2828=strmatch(%l,#1709)
&STATUS #2828=0|1|0
@set #2828/STATUS=no_command wizard mortal_dark
