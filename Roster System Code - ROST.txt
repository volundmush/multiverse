th u(NEWCOBJ,Roster System <ROST>,rost,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&CMD`+ROSTER [u(cobj,rost)]=$^(?s)(?\:\+)?(?\:roster)(?\:/(\S+)?)?(?\: +(.+?))?$:@attach %!/INC`GETSWITCH=%1;@attach %!/INC`[strfirstof(%q<switch>,MAIN)]=%2
@set [u(cobj,rost)]/CMD`+ROSTER=regexp

&SWITCHES`PLAYER [u(cobj,rost)]=FULL
&SYSTEM`NAME [u(cobj,rost)]=ROSTER

&INC`MAIN [u(cobj,rost)]=@switch/inline strlen(%0)=0,{@switch/inline words(u(setr,facs,setinter(get(%#/D`GROUPS),u(filter,FACTIONS,u(u(cobj,gms)/FUN`LISTGROUPS)))))=0,{@include %!/INC`UNAFFIL},1,{@include %!/INC`FACTION=%q<facs>},{@attach %!/INC`MSG=You are in more than one faction! Please specify what roster to check.}},{@include %!/INC`PARTIAL=%0,iter(u(setr,facs,u(filter,FACTIONS,u(u(cobj,gms)/FUN`LISTGROUPS))),name(%i0),,|)|Unaffiliated|Neutral,|,Faction,Faction;@switch/inline %q<faction>=UNAFFILIATED,{@include %!/INC`UNAFFIL},NEUTRAL,{@include %!/INC`NEUTRAL},{@include %!/INC`FACTION=namegrab(%q<facs>,%q<faction>)}}

&INC`FULL [u(cobj,rost)]=th u(setq,full,1);@include %!/INC`MAIN

&INC`FACTION [u(cobj,rost)]=@assert words(u(setr,roster,sortkey(#lambda/get(\%0/D`GROUP`%0`RANK),sort(lsearch(all,type,player,elock,D`GROUP`%0`RANK:[if(%q<full>,?*,<9)]),namei),n)))=@attach %!/INC`MSG=No players to show!;@pemit %#=u(HEADER,if(%q<full>,Full%b)Roster - [name(%0)]);@pemit %#=iter(lnum(1,div(u(width,%#),38)),Name[space(29)]Rank%B);@pemit %#=u(separator);th u(setq,gid1,%0);th step(FUN`FACTIONROSTER,%q<roster>,div(u(width,%#),38));@pemit %#=u(SUBHEADER)

&FUN`FACTIONROSTER [u(cobj,rost)]=pemit(%#,trim(iter(iter(lnum(0,sub(%+,1)),u(pueblize,u(setr,name,name(v(%i0))),+finger %q<name>)%B[rjust(left(u(u(cobj,gms)/FUN`FACRANK,%q<gid1>,v(%i0)),sub(37,strlen(%q<name>),1)),sub(37,strlen(%q<name>),1))],%b,|),ljust(%i0,38),|,%b)))

&INC`UNAFFIL [u(cobj,rost)]=th u(setq,facs,u(filter,FACTIONS,u(u(cobj,gms)/FUN`LISTGROUPS)));@assert words(u(setr,roster,setdiff(lsearch(all,eplayer,\[not(words(setinter(get(##/D`GROUPS),%q<facs>)))\]),lsearch(all,type,player,elock,FLAG^WIZARD|FLAG^ROYALTY|POWER^GUEST|POWER^BUILDER|FLAG^UNREGISTERED|OOC_CHARACTER:>0),,namei)))=@attach %!/INC`MSG=No players to show!;@pemit %#=u(HEADER,if(%q<full>,Full%b)Roster - Unaffiliated);@pemit %#=iter(lnum(1,div(u(width,%#),38)),Name[space(29)]Rank%B);@pemit %#=u(separator);th step(FUN`UNAFFILROSTER,%q<roster>,div(u(width,%#),38));@pemit %#=u(SUBHEADER)

&INC`NEUTRAL [u(cobj,rost)]=th u(setq,facs,setdiff(u(filter,FACTIONS,u(u(cobj,gms)/FUN`LISTGROUPS)),#2073));@assert words(u(setr,roster,setdiff(lsearch(all,eplayer,\[not(words(setinter(get(##/D`GROUPS),%q<facs>)))\]),lsearch(all,type,player,elock,FLAG^WIZARD|FLAG^ROYALTY|POWER^GUEST|POWER^BUILDER|FLAG^UNREGISTERED|OOC_CHARACTER:>0),,namei)))=@attach %!/INC`MSG=No players to show!;@pemit %#=u(HEADER,if(%q<full>,Full%b)Roster - Neutral);@pemit %#=iter(lnum(1,div(u(width,%#),38)),Name[space(29)]Rank%B);@pemit %#=u(separator);th step(FUN`UNAFFILROSTER,%q<roster>,div(u(width,%#),38));@pemit %#=u(SUBHEADER)

&FUN`UNAFFILROSTER [u(cobj,rost)]=pemit(%#,trim(iter(iter(lnum(0,sub(%+,1)),u(pueblize,u(setr,name,name(v(%i0))),+finger %q<name>)%B,%b,|),ljust(%i0,38),|,%b)))
