&CMD`+FLAWS [u(cobj,player_globals)]=$+flaws *:@break findplayer(%0); @assert words(filter(#lambda/hasattrval(%qp/%%0),flaws [u(lattr,%qp/flaws`page`*)]))=@pemit %#=ansi(r,name(%qp) has no flaws set. This probably means this character predates the inclusion of +flaws in the application.); @pemit %#=u(header,Flaws: [name(%qp)]); think u(#475/format`powerflaw,%qp,FLAWS); @pemit/silent %#=u(header,);@break cand(hasattrval(%qp,aflaws),not(u(isadmin,%#)))=@pemit %qp=%n just checked your flaws.;@switch/inline approved(%qp)=0,{@pemit %#=ansi(hr,These +flaws are not approved.)};@select/inline u(isadmin,%#)=0,{@trigger %qp/AFLAWS=%#,u(moniker,%#)}

&CMD`+FLAWSNOARG [u(cobj,player_globals)]=$+flaws:think u(setq,p,%#); @assert words(filter(#lambda/hasattrval(%qp/%%0),flaws [u(lattr,%qp/flaws`page`*)]))=@pemit %#=ansi(r,You have no flaws.); @pemit/silent %#=u(header,Flaws: [name(%qp)]); think u(#475/format`powerflaw,%qp,FLAWS); @pemit/silent %#=u(header,);@switch/inline approved(%qp)=0,{@pemit %#=ansi(hr,These +flaws are not approved.)};

@@ &CMD`FORMATTER [u(cobj,player_globals)]=$+formatter *:think u(setq,l,sort(get(#1123/valid`formatters),name)); @break isnum(%0)=@pemit %#=if(cand(gte(%0,1),lte(%0,words(%ql))),attrib_set(%#/adminattrs`formatter,u(setr,f,extract(%ql,%0,1)))Formatter set to [name(%qf)].,Invalid formatter. Must be either the name of the formatter or a number from 1 to [words(%ql)].); @assert u(setr,f,namegraball(%ql,%0))=@pemit %#=No matches for '%0'. Try a more specific name.; @break gt(words(%qf),1)=@pemit %#=Multiple matches:%r[iter(%qf,match(%ql,%i0). [name(%i0)],,%r)]; @pemit %#=attrib_set(%#/adminattrs`formatter,%qf)Formatter set to [name(%qf)].

@@ &CMD`FORMATTERS [u(cobj,player_globals)]=$+formatters:@pemit %#=%rFormatters:%r[iter(sort(get(#1123/valid`formatters),name),inum(0). [name(%i0)],,%r)]%r%rCurrently using: [name(default(%#/adminattrs`formatter,get(#1123/default`formatter)))].%rUse '+formatter <name or number>' to set the formatter.%r

&CMD`RANKS [u(cobj,player_globals)]=$ranks:think u(setq,l,u(sortname,lvplayers(%l))));@pemit %#=u(header,Ranks);@pemit %#=ansi(u(color,%#,COLOR,COLUMN_NAMES),align(22 20 25 4,Name,Faction,Rank,Idle));@pemit %#=u(separator);@dolist/inline %ql={@pemit %#=align(22 20 25 4,u(pueblize,u(moniker,%i0),+finger [name(%i0)]),u(getproperty,%i0,majornamelist,1,,1),u(getproperty,%i0,majorranklist,,,1),u(hideidle,%i0))};@pemit %#=u(subheader)

&CMD`FACHEAD [u(cobj,player_globals)]=$+fachead:th u(setq,facs,u(sortname,u(filter,MAJOR,u(u(cobj,gms)/FUN`LISTGROUPS))));@pemit %#=u(header,Fachead List - [mudname()]);@pemit %#=ansi(u(color,%#,COLOR,COLUMN_NAMES),align(3 18 19 28 4,Con,Faction,Name,Position,Duty));@pemit %#=u(SEPARATOR);@dolist/inline %q<facs>={th u(setq,gid1,%i0);@dolist/inline u(u(cobj,gms)/SORTRANK,u(sortname,lsearch(all,type,player,elock,D`GROUP`%i0`RANK:<3)))=@pemit %#=align(3 18 19 28 4,u(hideconn,%i0),u(getproperty,%i1,GROUPNAME),u(pueblize,u(moniker,%i0),+finger [name(%i0)]),u(getproperty,%i0,MAJORRANK),default(%0/duty,Off))};@pemit %#=u(separator);@pemit %#=u(game_config,STAFF,FACHEAD_FOOTER);@pemit %#=u(subheader)

&CMD`XWHO [u(cobj,player_globals)]=$xwho:th u(setq,who,u(lwhoid,%#));@pemit %#=u(header,Faction Who);@dolist/inline/nobreak u(setr,facs,u(filter,MAJOR,u(u(cobj,gms)/FUN`LISTGROUPS)))={@assert words(u(setr,members,setinter(%q<who>,get(%i0/MEMBERS),,namei)));@pemit %#=u(separator,u(getproperty,%i0,GROUPNAME));@pemit %#=u(table,iter(%q<members>,u(pueblize,u(moniker,%i0),+finger [name(%i0)]),%b,|),18,u(width,%#),|)};th iter(%q<facs>,u(setr,unaff,setdiff(strfirstof(%q<unaff>,%q<who>),get(%i0/MEMBERS))));th u(setq,ooc,u(sortname,filterbool(#lambda/cor(haspower(\%0,GUEST),u(isadmin,\%0)),%q<unaff>)));th u(setq,unaff,setdiff(%q<unaff>,%q<ooc>,,namei));@switch/inline t(words(%q<unaff>))=1,{@pemit %#=u(separator,Unaffiliated);@pemit %#=u(table,iter(%q<unaff>,u(pueblize,u(moniker,%i0),+finger [name(%i0)]),%b,|),18,u(width,%#),|)};@switch/inline t(words(%q<ooc>))=1,{@pemit %#=u(separator,OOC);@pemit %#=u(table,iter(%q<ooc>,u(pueblize,name(%i0),+finger [name(%i0)]),%b,|),18,u(width,%#),|)};@pemit %#=u(subheader)

&CMD`+IDLE [u(cobj,player_globals)]=$^\+idle(\s+(.+))?$:@select/inline hasattr(%#,idle)[switch(%2,,u(setq,r,Idle)1,u(setq,r,%2)0)]=11,{@remit/silent %l=u(setq,m,%N returns from: [get(%#/idle)])[uldefault(%l/format-ooc,<OOC> %qm,%qm,%#)];@idle %#;@pemit %#=ansi(h,Your RP status is currently:) [ulocal(#3326/fun`status,%#)]},10,{@pemit %#=Your idle has been updated to: %qr;@idle %#=%qr},0?,{@remit/silent %l=u(setq,m,%N goes idle: %qr)[uldefault(%l/format-ooc,<OOC> %qm,%qm,%#)];@idle %#=%qr}
@set [u(cobj,player_globals)]/CMD`+IDLE=regexp

&ACONNECT [u(cobj,player_globals)]=@assert haspower(%#,guest);@@ @wait 5=@pemit %#=Welcome to Multiverse Crisis MUSH.  Due to problems with trolls and spammers we ask that you please keep the over-use of the guest channel down, as well as avoid excessively silly topics such as trying to apply for a sandwich (similar events have actually happened).  To use the guest channels type '+g Hello' without the single quotes.  If our staffers seem a bit strict about this please don't mistake this for unfriendliness - we're just trying to keep things sane and keep the guest channel from being entirely clogged with things.  If you have any questions contact an admin.  Thank you!%r%r[center(ansi(h,>>> Please read our wiki at http://www.multiversemush.com/ <<<),78)]

&GFN`GETFACTION [u(cobj,player_globals)]=vol_getproperty(%0,faction)

&GFN`GETSPECIES [u(cobj,player_globals)]=vol_getproperty(%0,species)

&MUSHBASIC2_CMD [u(cobj,player_globals)]=$mushbasic2:@pemit %#=MOVEMENT%rMovement is accomplished by looking at the exits located at the bottom of a room's description.  To look at the description fo the room you're standing in, type 'look'.%r%rYou should see something like this:%r%rA Room%rThis is the room description!%rContents:%rAn Object%r<O>ut leads to Another Room%r<I>n leads to Road 47%r%rIn order to move to one of the other rooms,  you would either use 'O' for Out, or 'I', for In.  These are called 'exits' and can be named anything, but are most commonly <E>ast, <S>outh, <N>orth, <W>est, <I>n, <O>ut, <U>p, or <D>own.  Note that when there's a character or characters in the exit name or in front of the exit name, this is a shortcut as opposed to typing out the entire exit name.%r%rType 'mushbasic3' to continue.
&MUSHBASIC3_CMD [u(cobj,player_globals)]=$mushbasic3:@pemit %#=CHANNELS%rChannels are like a small, built-in chat system.  There are several channels on the MUSH, both to facilitate OOC (Out Of Character) and IC (In Character) communication.  here are the most important commands:%r%r@chan/list - lists all available channels.%r@chan/on <channel> - join channel named <channel>.%r@chan/off <channel> - leave channel named <channel>.%r@chan/gag <channel> - gag channel named <channel>.%r@chan/ungag <channel> - ungag channel named <channel>.%r%rChannels can be spoken on by using a + character, combined with the first few letters of the channel name.  For example, to speak on Public, you might type '+pub Hi', which would result in <Public> Jim says, "Hi".  Note that Guests can only speak on the Guest channel.
&MUSHBASIC_CMD [u(cobj,player_globals)]=$mushbasic:@pemit %#=MUSH Basics%r%rBasic commands:%rsay <phrase> - makes you say <phrase>, i.e. say Hi -> Jim says, "Hi."%rpose <pose> - makes you 'do' something, i.e. pose waves -> Jim waves%rlook - shows you the contents of a room and it's description%rlook <target> - shows you the contents and description of <target>, i.e. look joe shows you joe's description.%r%rObjects:%rtake <object> - pick up <object>.  Note that objects do not have special powers - this is not a MUD - it's often rude to pick up other's objects without permission.%rdrop <object> - drop <object> if you have it in your inventory.  Note that objects do not have special powers - this is not a MUD.%renter <object> - enter <object>, if you can enter that object.%rleave - leave an object you entered with object.%r%rhelp - shows basic MUSH help.%r+help - shows commands extended to the MUSH command set on this particular MUSH.%rnews - shows files about the particular MUSH you are on.%r%r+bbread - lets you read the bulletin board system%r+bbread <#> - lets you read board #%r+bbread <#>/A - lets you read post A on board #%rFor more help on the bulletin board system, read +help bb%r%rType 'mushbasic2' to continue.

&CMD`RWHO [u(cobj,player_globals)]=$^(?\:rwho|fwho)(?\: +(.+?))?$:@switch/inline strlen(%1)=0,{@assert strlen(u(setr,fac,getproperty(%#,faction)))=@pemit %#=u(MSGHEAD,WHO) You are not in a faction!;@force/inline %#=+gwho %q<fac>},{@force/inline %#=+gwho %1}
@set [u(cobj,player_globals)]/CMD`RWHO=regexp
&CMD`IWHO [u(cobj,player_globals)]=$iwho:@force/inline %#=+who/idle

&CMD`DICE [u(cobj,player_globals)]=$+dice *d*:think [u(setq,0,0)][if(strmatch(%1,*+*),[u(setq,a,[before(%1,+)])][u(setq,b,[after(%1,+)])],[u(setq,a,%1)][u(setq,b,0)])] [switch([and([gt(%0,0)],[lt(%0,50)],[gt(%qa,0)],[lt(%qa,250)],[gte(%qb,-10)],[lt(%0,250)],[isint(%0)],[isint(%qa)],[isint(%qb)])],0,[pemit(%#,I'm sorry but you can only have positive integers when you roll dice. The addition at the end must be between -10 and 250.)],[remit([loc(%#)],<[ansi(xterm130,DICE ROLLER)]> %N rolled %0d%qa [map(me/full_die,[lnum(%0)], ,[space(1)])] + %qb = [if(%qb,[u(setq,0,[add(%q0,%qb)])])]%q0)])]
&FULL_DIE [u(cobj,player_globals)]=[u(setq,1,add(rand(%qa),1))][u(setq,0,add(%q0,%q1))]<%q1>

&CMD`+LAST [u(cobj,player_globals)]=$+last *:@switch/inline [u(setr,0,[fullname(*%0)])]=#-1 NO SUCH OBJECT VISIBLE,{@pemit %#=No such player.},{@pemit %#=%q0 last connected: [get(*%q0/LAST)]}

&CMD`CHARID [u(cobj,player_globals)]=$^\+charid(?\: +(.*))?$:@force/inline %#=+scene/charid %1
@set [u(cobj,player_globals)]/CMD`CHARID=regexp