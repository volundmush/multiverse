&VAR`ADMINFLAGS u(jobsys)=INSTALL|NEWCATEGORY|DELCATEGORY|RENCATEGORY|LOCKCATEGORY|UNLOCKCATEGORY|CATEGORY|CONFIG|DESC|SHOWSUBMIT
&INC`SHOWSUBMIT u(jobsys)=@assert hasflag(%#,WIZARD)=@nspemit %#=Huh?  (Type "help" for help.);@assert setr(jid,u(SQL,GET`JOBID,%0))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Job %0 not found.;@nspemit %#=Job was submitted by: [u(SQL,GET`OWNERNAME,%q<jid>)];@nscemit/noisy Wizard=ansi(hr,ALERT:) [ansi(h,%n)] just used /showsubmit on Job %q<jid>!

&FUN`LIST`OWNERNAME u(jobsys)=localize(if(strmatch(setr(owner,u(SQL,GET`OWNEROBJID,%0)),%:),u(SQL,GET`OWNERNAME,%0),switch(u(FUN`CONF,u(SQL,GET`JOBCAT,%0),ANONYMOUS),0,u(SQL,GET`OWNERNAME,%0),1,if(cand(isadmin(%#),match(iter(get(u(adb)/25`OLD),before(%i0,~),|,%b),%q<owner>)),Anonymous [ansi(hc,\([u(SQL,GET`OWNERNAME,%0)]\))],Anonymous),3,if(cand(isadmin(%#),match(iter(get(u(adb)/25`OLD),before(%i0,~),|,%b),%q<owner>)),Anonymous [ansi(hc,\([u(SQL,GET`OWNERNAME,%0)]\))],Anonymous),u(SQL,GET`OWNERNAME,%0))))

&INC`DISPLAYJOB u(jobsys)=@assert setr(jid,u(SQL,GET`JOBID,%0))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Job %0 not found.;@assert or(strmatch(setr(ownobj,u(SQL,GET`OWNEROBJID,%q<jid>)),%:),match(setr(playerobj,u(SQL,GET`PLAYERS,%q<jid>)),%:),setr(catadmview,or(match(setr(claimobj,u(SQL,GET`CLAIMOBJIDS,%q<jid>)),%:),u(FUN`PERMCHECK,setr(cat,u(SQL,GET`JOBCAT,%q<jid>)),ADMIN,%#))))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Permission Denied.;@nspemit %#=u(RFN`HEADER,mudname() Jobs - Job %q<jid>);@nspemit %#=align(36 36,ljust(rjust(ansi(h,Category:),10),10) [u(FUN`LIST`CATEGORY,%q<jid>)],ljust(rjust(ansi(h,Due:),10),10) [strfirstof(u(FUN`LIST`JOBDUE2,%q<jid>),---)]);@nspemit %#=align(36 36,ljust(rjust(ansi(h,Title:),10),10) [u(FUN`LIST`JOBTITLE,%q<jid>)],ljust(rjust(ansi(h,Status:),10),10) [u(FUN`LIST`STATUS2,%q<jid>)]);@nspemit %#=align(36 36,ljust(rjust(ansi(h,Watchers:),10),10) [u(FUN`LIST`WATCHERS,%q<jid>)],);@nspemit %#=u(RFN`SEPARATOR);@nspemit %#=ansi(h,u(FUN`LIST`OWNERNAME,%q<jid>) Opened on [fancytime(u(FUN`LIST`SUBMIT2,%q<jid>),%#)] with:);@nspemit %#=u(FUN`LIST`TEXT,%q<jid>);th setq(anon,u(FUN`CONF,%q<cat>,ANONYMOUS));th mapsql(FUN`COMMENTS,u(FUN`SQLFORMAT,GET`COMMENTS[if(cor(u(FUN`PERMCHECK,%q<cat>,ADMIN,%#),match(u(SQL,GET`CLAIMOBJIDS,%q<jid>),%:)),ALL)],%q<jid>));@nspemit %#=u(RFN`HEADER);@include u(jobsys)/INC`DOSQL=SET`CHECKDATE,%q<jid>,%q<pid>


&SHLP`+JOB u(jobsys)=[ansi(hc,Staff Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+job/category <id>=<category>)] - Moves a job to a new category.})]%R%R[ansi(hc,Wizard Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+jobs/install)] - Installs the SQL tables the +job system uses. Does nothing if they are already installed.%R[ansi(h,+job/newcategory <name>)] - Creates a new category.%R[ansi(h,+job/delcategory <name>)] - Deletes a category.%R[ansi(h,+job/rencategory <name>=<newname>)] - Renames a category.%R[ansi(h,+job/desc <name>=<description>)] - Set a category's description in +job%R[ansi(h,+job/lockcategory <name>/<lock>=<lockstring>)] - Lock a category. <lock> can be ADMIN or POST for who can administrate a category and who can +request to it. See +help +key for more information on <lockstrings>.%R[ansi(h,+job/unlockcategory <category>/<lock>)] - As above. Unlocks.%R[ansi(h,+job/config <category>/<option>=<value>)] - Sets a category's config. <option> can be Anon or Due.%R[ansi(h,DUE)] - Set to a value acceptable by stringsecs such as 5d or 50m. This is added to submit date to create the due date.%R[ansi(h,ANON)] - Modes 0-3. Mode 0 is fully transparent. Mode 1 hides submitter from staff. Mode 2 hides staff from submitter. Mode 3 is 1+2 combined.%R[ansi(h,+job/showsubmit <id>)] - Display the person who submitted a job. used for emergencies when Anonymity backfires.})]