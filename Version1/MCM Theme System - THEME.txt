@switch/inline isdbref(u(theme))=0,{@tel create(MCM Theme System <THEME>)=config(master_room)}
&theme u(coi)=locate(config(master_room),MCM Theme System <THEME>,TXxi)
@parent u(theme)=u(coi)
@set u(theme)=WIZARD SAFE !NO_COMMAND

@switch/inline isdbref(u(tdb))=0,{@tel create(MCM Theme Database <TDB>)=u(theme)}
&tdb u(coi)=locate(u(theme),MCM Theme Database <TDB>,TXxi)
@parent u(tdb)=u(coi)
@power u(tdb)=many_attribs
@set u(tdb)=WIZARD SAFE !NO_COMMAND

&RFN`MSGHEAD u(theme)=msghead(v(VAR`MSGHEAD))
&RFN`HEADER u(theme)=header(%0,,theme`BORDER,theme`BORDERDOT,theme`BORDERTEXT)
&RFN`SUBHEADER u(theme)=subheader(%0,,theme`BORDER,theme`BORDERDOT,theme`BORDERTEXT)
&RFN`SEPARATOR u(theme)=separator(%0,,theme`BORDER,theme`BORDERDOT,theme`BORDERTEXT)
th attrib_set(u(cco-db)/VAR`CATEGORIES,setunion(get(u(cco-db)/VAR`CATEGORIES),THEME,|,|))
&VAR`CATEGORIES`THEME u(cco-db)=BORDER|BORDERTEXT|BORDERDOT|COLNAME
&VAR`MSGHEAD u(theme)=THEME

&CMD`+THEME u(theme)=$^(?s)(?\:\+)?theme(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.+)?)?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(theme)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(theme)/VAR`ADMINFLAGS)),|,|),|,v(VAR`MSGHEAD),switch,switch;@include u(theme)/INC`[strfirstof(%q<switch>,MAIN)]=%2,%3
@set u(theme)/CMD`+THEME=regexp

&VAR`PLAYFLAGS u(theme)=POWERS|CAST|LIST
&VAR`ADMINFLAGS u(theme)=ADD|GET|SETPOWERS|CLEARPOWERS|SETDESC|RENAME|DELETE|ASSIGN|REMOVE|UNASSIGNED|EMPTY

&INC`MAIN u(theme)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) Which theme would you like to check?;@include u(theme)/INC`FINDTHEME=%0,1;@nspemit %#=u(RFN`HEADER,Theme: %q<themename1>);@nspemit %#=get(u(tdb)/%q<theme1>`DESC);@switch/inline t(words(setr(cast,get(u(tdb)/%q<theme1>))))=1,{@include u(theme)/INC`SHOWCAST=%q<theme1>,Cast};@switch/inline gt(strlen(get(u(tdb)/%q<theme1>`POWERS)),0)=1,{@nspemit %#=u(RFN`SUBHEADER,pueblize(This theme has powers!,+theme/powers %q<themename1>))},0,{@nspemit %#=u(RFN`SUBHEADER)}

&INC`LIST u(theme)=@assert words(setr(themes,sortkey(#lambda/get(u(tdb)/\%0),reglattr(u(tdb)/^\\d+$),i)))=@nspemit %#=u(RFN`MSHEAD) No inhabited themes to display.;@nspemit %#=u(RFN`HEADER,mudname() - All Themes);th step(FUN`LISTTHEMES,%q<themes>,div(width(%#),37));@nspemit %#=u(RFN`SUBHEADER)

&FUN`LISTTHEMES u(theme)=nspemit(%#,table(iter(lnum(0,sub(%+,1)),pueblize(ansi(custcolor(%#,THEME`NAME),setr(name,get(u(tdb)/[v(%i0)]))),+theme %q<name>),%b,|),37,width(%#),|))

&INC`SHOWCAST u(theme)=@nspemit %#=u(RFN`SEPARATOR,strfirstof(%1,pueblize(get(u(tdb)/%0),+theme [get(u(tdb)/%0)])));@nspemit %#=ansi(firstcolor(%#,THEME`COLNAME,DEFAULT`COLNAME),align(3 20 1 3 18 21 >6,Typ,Name,G,PL,Faction,Rank,LastOn));@dolist/inline sort(filterbool(#lambda/cand(isobjid(\%0),not(isadmin(\%0))),get(u(tdb)/%0`CAST)),namei)={@nspemit %#=align(3 20 1 3 18 21 >6,default(%i0/D`FINGER`CHARTYPE,??),pueblize(name(%i0),+finger [name(%i0)]),switch(get(%i0/sex),m*,M,f*,F,n*,N,?),default(%i0/level,??),strfirstof(getproperty(%i0,factions,1,,1),Unaffiliated),getproperty(%i0,ranks,,,1),if(cand(gte(conn(%i0),0),not(hidden(%i0))),ansi(hg,ago(idle(%i0))),ansi(hx,elements(get(%i0/LAST),2 3))))}

&INC`CAST u(theme)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) Which casts would you like to see?;@assert words(setr(themes,sortkey(#lambda/get(u(tdb)/\%0),u(FUN`FINDMATCHES,%0),i)))=@nspemit %#=u(RFN`MSGHEAD) No themes matched '%0'.;@assert words(setr(show,sortkey(#lambda/get(u(tdb)/\%0),filterbool(#lambda/strlen(get(u(tdb)/\%0`CAST)),%q<themes>),i)))=@nspemit %#=u(RFN`MSGHEAD) Themes [itemize(iter(%q<themes>,pueblize(get(u(tdb)/%i0),+theme [get(u(tdb)/%i0)]),,|),|,and,\,)] have no players.;@nspemit %#=u(RFN`HEADER,Casts for: %0);@dolist/inline %q<show>={@include u(theme)/INC`SHOWCAST=%i0};@nspemit %#=u(RFN`HEADER)

&INC`GET u(theme)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) Which theme would you like to check?;@include u(theme)/INC`FINDTHEME=%0,1;@nspemit %#=u(RFN`HEADER,Theme: %q<themename1>);@nspemit %#=decompose(get(u(tdb)/%q<theme1>`DESC));@nspemit %#=u(RFN`SUBHEADER)

&INC`POWERS u(theme)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) Which theme would you like to check?;@include u(theme)/INC`FINDTHEME=%0,1;@assert gt(strlen(setr(powers,get(u(tdb)/%q<theme1>`POWERS))),0)=@nspemit %#=u(RFN`MSGHEAD) %q<themename1> has no powers.;@nspemit %#=u(RFN`HEADER,Theme Powers: %q<themename1>);@nspemit %#=get(u(tdb)/%q<theme1>`POWERS);@nspemit %#=u(RFN`SUBHEADER)

&INC`ADD u(theme)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Name field empty.;@break words(u(u(theme)/FUN`FINDMATCH,%0))=@nspemit %#=u(RFN`MSGHEAD) ERROR: That theme already exists. Use +theme/setdesc to change its description.;@assert strlen(%1)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Theme description field empty.;&[setr(attr,nextslot(u(tdb)))] u(tdb)=%0;&%q<attr>`DESC u(tdb)=%1;@nspemit %#=u(RFN`MSGHEAD) You have created the '%0' Theme.;@nscemit/noisy u(CMO`STAFFREP)={ansi(h,%n) created a new +theme entry: '[pueblize(%0,+theme %0)]'}

&INC`SETDESC u(theme)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Name field empty.;@include u(theme)/INC`FINDTHEME=%0,1;@assert strlen(%1)=@nspemit %#=u(RFn`MSGHEAD) ERROR: Theme description field empty.;@switch/inline setr(mail,gt(strlen(setr(olddesc,get(u(tdb)/%q<theme1>`DESC))),0))=1,{@mail %#=%q<themename1>/OLD THEMELIST WAS:%R%q<olddesc>;&%q<theme1>`DESC u(tdb)=%1};@nspemit %#=u(RFN`MSGHEAD) You have changed theme %q<themename1>'s description. Its old contents have been @mail'd to you.;@nscemit/noisy u(CMO`STAFFREP)={ansi(h,%n) changed the Theme description for '[pueblize(%q<themename1>,+theme %q<themename1>)]'}

&INC`SETPOWERS u(theme)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Name field empty.;@include u(theme)/INC`FINDTHEME=%0,1;@assert strlen(%1)=@nspemit %#=u(RFn`MSGHEAD) ERROR: Theme powers field empty.;@switch/inline setr(mail,gt(strlen(setr(oldpowers,get(u(tdb)/%q<theme1>`POWERS))),0))=1,{@mail %#=%q<themename1>/OLD POWERS WERE:%R%q<oldpowers>};&%q<theme1>`POWERS u(tdb)=%1;@nspemit %#=u(RFN`MSGHEAD) You have changed theme %q<themename1>'s powers.[if(%q<mail>,%bIts old contents have been @mail'd to you.)];@nscemit/noisy u(CMO`STAFFREP)={ansi(h,%n) changed the Theme powers for '[pueblize(%q<themename1>,+theme %q<themename1>)]'}

&INC`CLEARPOWERS u(theme)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Name field empty.;@include u(theme)/INC`FINDTHEME=%0,1;@assert strlen(get(u(tdb)/%q<theme1>`POWERS))=@nspemit %#=u(RFn`MSGHEAD) ERROR: Theme has no powers.;@mail %#=%q<themename1>/OLD POWERS WERE:%R[get(u(tdb)/%q<theme1>`POWERS)];&%q<theme1>`POWERS u(tdb);@nspemit %#=u(RFN`MSGHEAD) You have deleted theme %q<themename1>'s powers. Its old contents have been @mail'd to you.;@nscemit/noisy u(CMO`STAFFREP)={ansi(h,%n) deleted the Theme powers for '[pueblize(%q<themename1>,+theme %q<themename1>)]'}

&INC`DELETE u(theme)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Name field empty.;@include u(theme)/INC`FINDTHEME=%0,1;@include u(ccs)/INC`VERIFY={ansi(hr,WARNING:) This will delete Theme '%q<themename1>. Are you SURE you want to do this? Enter the same command again within 10 seconds to verify!},DELETE THEME %q<theme1>,v(VAR`MSGHEAD);@switch/inline setr(mail,t(words(setr(cast,filterbool(#lambda/isobjid(\%0),sort(get(u(tdb)/%q<theme1>`CAST),namei))))))=1,{@switch/inline t(strlen(setr(oldpowers,get(get(u(tdb)/%q<theme1>`POWERS)))))=1,{@mail %#=%q<themename1>/OLD POWERS WERE:%R%q<oldpowers>};@switch/inline t(strlen(setr(olddesc,get(u(tdb)/%q<theme1>`DESC))))=1,{@mail %#=%q<themename1>/OLD THEMELIST WAS:%R%q<olddesc>};@mail %#=%q<themename>/OLD CAST WAS:%R[itemize(iter(%q<cast>,name(%i0),%b,|),|,and,\,)]};@wipe u(tdb)/%q<theme1>;@nspemit %#=u(RFN`MSGHEAD) You have deleted theme %q<themename1>![if(%q<mail>,%bIts old contents have been @mail'd to you.)];@nscemit/noisy u(CMO`STAFFREP)={ansi(h,%n) [ansi(hr,DELETED)] Theme '[pueblize(%q<themename1>,+theme %q<themename1>)]'}

&INC`RENAME u(theme)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Name field empty.;@include u(theme)/INC`FINDTHEME=%0,1;@assert strlen(%1)=@nspemit %#=u(RFn`MSGHEAD) ERROR: New name field empty.;@break u(u(theme)/FUN`FINDMATCH,%1)=@nspemit %#=u(RFn`MSGHEAD) ERROR: A theme called '%1' already exists.;&%q<theme1> u(tdb)=%1;@nspemit %#=u(RFN`MSGHEAD) You have changed theme %q<themename1>'s named to '%1';@nscemit/noisy u(CMO`STAFFREP)={ansi(h,%n) changed the Theme name of '%q<themename1>' to '[pueblize(%1,+theme %1)]'}

&FUN`FINDMATCH u(theme)=wildgrepi(u(tdb),*,%0)
&FUN`FINDMATCHES u(theme)=grepi(u(tdb),*,%0)
&FUN`FINDPLAYER u(theme)=before(first(grepi(u(tdb),*`CAST,objid(%0))),`)

&INC`ASSIGN u(theme)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: No player entered to assign.;@include u(ccs)/INC`CHECKPC=%0,1,v(VAR`MSGHEAD);@break setr(old,u(u(theme)/FUN`FINDPLAYER,%q<t1>))=@nspemit %#=u(RFN`MSGHEAD) ERROR: They already belong to '[get(u(tdb)/%q<old>)]'.;@include u(theme)/INC`FINDTHEME=%1,1;&%q<theme1>`CAST u(tdb)=filterbool(#lambda/isobjid(\%0),setunion(get(u(tdb)/%q<theme1>`CAST),%q<t1objid>));@nspemit %#=u(RFN`MSGHEAD) You assign %q<t1name> to Theme '%q<themename1>';@nscemit/noisy u(CMO`STAFFREP)={ansi(h,%n) assigned %q<t1name> to Theme '[pueblize(%q<themename1>,+theme %q<themename1>)]'};&D`THEME %q<t1>=%q<theme1>

&INC`REMOVE u(theme)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: No player entered to assign.;@include u(ccs)/INC`CHECKPC=%0,1,v(VAR`MSGHEAD);@assert setr(old,u(u(theme)/FUN`FINDPLAYER,%q<t1>))=@nspemit %#=u(RFN`MSGHEAD) ERROR: They do not belong to any theme!;@include u(theme)/INC`FINDTHEME=get(u(tdb)/%q<old>),1;&%q<theme1>`CAST u(tdb)=filterbool(#lambda/isobjid(\%0),setdiff(get(u(tdb)/%q<theme1>`CAST),%q<t1objid>));@nspemit %#=u(RFN`MSGHEAD) You remove %q<t1name> from Theme '%q<themename1>';@nscemit/noisy u(CMO`STAFFREP)={ansi(h,%n) removed %q<t1name> from Theme '[pueblize(%q<themename1>,+theme %q<themename1>)]'};&D`THEME %q<t1>

&INC`UNASSIGNED u(theme)=@assert words(setr(unassigned,sort(lsearch(all,type,player,elock,!D`THEME:?*&!POWER^GUEST&!(FLAG^WIZARD|FLAG^ROYALTY)),namei)))=@nspemit %#=u(RFN`MSGHEAD) No players lack theme assignments.;@nspemit %#=u(RFN`HEADER,Unassigned Players);@nspemit %#=align(20 30,Name,Creation Time);@dolist/inline %q<unassigned>={@nspemit %#=align(20 30,pueblize(name(%i0),+finger [name(%i0)]),ctime(%i0));@switch/inline eq(inum(0),words(%q<unassigned>))=1,{@nspemit %#=u(RFN`HEADER)}}

&INC`FINDTHEME u(theme)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Name field empty.;@switch/inline t(setr(themecheck,u(u(theme)/FUN`FINDMATCH,%0)))=0,{@assert words(setr(themecheck,u(u(theme)/FUN`FINDMATCHES,%0)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Theme '%0' was not found.;@break gt(words(%q<themecheck>),1)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Multiple Themes found with a similar name. Which do you mean? Matches: [itemize(iter(%q<themecheck>,pueblize(get(u(tdb)/%i0),+theme [get(u(tdb)/%i0)]),%b,|),|,and,\,)]};th setq(theme%1,%q<themecheck>);th setq(themename%1,get(u(tdb)/%q<themecheck>))

&INC`EMPTY u(theme)=@assert gt(words(setr(empty,sort(filterbool(FUN`EMPTY,reglattr(u(tdb)/^\\d+$)),n))),0)=@nspemit %#=u(RFN`MSGHEAD) There are no empty themes.;@nspemit %#=u(RFN`MSGHEAD) The following themes have no cast: [itemize(iter(%q<empty>,pueblize(get(u(tdb)/%i0),+theme [get(u(tdb)/%i0)]),,|),|,and,\,)]

&FUN`EMPTY u(theme)=not(words(filterbool(#lambda/isobjid(\%0),get(u(tdb)/%0`CAST))))

&OBJECT`DESTROY`PURGETHEME u(theme)=@switch/inline %2=PLAYER,{@dolist/inline grepi(u(tdb),*,%0)={&%i0`CAST u(tdb)=filterbool(#lambda/isobjid(\%0),get(u(tdb)/%i0`CAST))}

&STARTUP u(theme)=@trigger u(theme)/TRG`REPORTEMPTY

&TRG`REPORTEMPTY u(theme)=@switch/inline gt(words(setr(empty,sort(filterbool(FUN`EMPTY,reglattr(u(tdb)/^\\d+$)),n))),0)=1,{@nscemit/noisy u(cmo`staffrep)={EMPTY THEMES: The following themes have no Cast: [itemize(iter(%q<empty>,pueblize(get(u(tdb)/%i0),+theme [get(u(tdb)/%i0)]),,|),|,and,\,)]}};@wait mul(60,60,6)=@trigger u(theme)/TRG`REPORTEMPTY

+help/addmain Community/+theme=[u(theme)]/HLP`+THEME
&HLP`+THEME u(theme)=The +theme system keeps track of [mudname()]'s many themes in play!%R%R[ansi(hc,Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+theme/list)] - Lists all +themes in play. Very spammy!%R[ansi(h,+theme <name>)] - Lists a specific theme and its cast.%R[ansi(h,+theme/cast <text>)] - Lists the casts of all themes that partially match <text>.%R[ansi(h,+theme/powers <theme>)] - Lists any shared powers for a theme.%RAll commands accept partial matches and will warn about ambiguity if need be.})]%R%R[ansi(hc,Old Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+themelist)] - Alternate for +theme/list%R[ansi(h,+themelist <name)] - Alternate for +theme <name>%R[ansi(h,+themelist/powers <name>)] - Alternate for +theme/powers <name>%R[ansi(h,+cast <text>)] - Alternate for +theme/cast <text>})]

+shelp/addmain Community/+theme=[u(theme)]/SHLP`+THEME
&SHLP`+THEME u(theme)=The +theme system keeps track of [mudname()]'s many themes in play!%R%R[ansi(hc,Staff Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+theme/add <name>=<description>)] - Creates a new theme.%R[ansi(h,+theme/setdesc <theme>=<new description>)] - Overwrites a description.%R[ansi(h,+theme/setpowers <Theme>=<new powers>)] - Sets or overwrites theme powers.%R[ansi(h,+theme/assign <character>=<theme>)] - Places a character in a theme.%R[ansi(h,+theme/remove <character>)] - Remove a character from whatever theme they're in.%R[ansi(h,+theme/unassigned)] - Find all characters who aren't linked to a theme.%R[ansi(h,+theme/delete <theme>)] - Delete a theme.%R[ansi(h,+theme/clearpowers <theme>)] - Erases a theme's +theme/powers.%R[ansi(h,+theme/rename <theme>=<new name>)] - Renames a theme. Should update on all characters!})]%R%R[ansi(hc,Old Commands)]%RThese still work! They're just shortcuts though.%R[align(5 [sub(width(%#),6)],,{[ansi(h,+addtheme <theme>=<description>)] - Alternate for +theme/add. Will not overwrite anymore, you'll need to use +theme/setdesc.%R[ansi(h,+Addtheme/powers <theme>=<powers>)] - Alternate for +theme/setpowers.%R[ansi(h,+setseries <player>=<theme>)] - Alternate for +theme/assign})]


&HEADER #3030=separator([ansi(hw,< [capstr(%1)]: [name(%0)])] >)
&FOOTER #3030=header()


@@ SHORTCUTS
&CMD`ADDTHEMEPOWERS u(sgo)=$+addtheme/powers *=*:@force/inline/noeval %#=+theme/setpowers %0=%1
&CMD`SETSERIES u(cmm)=$+setseries *=*:@force/inline/noeval %#=+theme/assign %0=%1
&CMD`ADDTHEME u(sgo)=$+addtheme *=*:@force/inline/noeval %#=+theme/add %0=%1
&CMD`DELTHEME u(sgo)=$+deltheme *:@force/inline/noeval %#=+theme/delete %0
&CMD`THEMELIST u(pgo)=$+themelist:@force/inline/noeval %#=+theme/list
&CMD`THEMELISTARG u(pgo)=$+themelist *:@force/inline/noeval %#=+theme %0
&CMD`THEMELISTPOWERS u(pgo)=$+themelist/powers *:@force/inline/noeval %#=+theme/powers %0
&CMD`CAST u(pgo)=$+cast *:@force/inplace %#=+theme/cast %0