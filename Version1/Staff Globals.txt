&CMD`AINFO u(sgo)=$+oldainfo *:think setq(d,#706); @assert regmatch(%0,%(?sm%)^%(%[^/%]+%)%(?:/%(%[^=%]+%)=%(.+%)|/%(%[0-9%]+%)%)?$,-1 0 s t i)=@pemit %#=Invalid syntax.; @break cand(not(isobjid(%q0)),hasattr(%qd,db`[setr(a,edit(%q0,:,_))]),not(strmatch(%qs,)))=@pemit %#=attrib_set(%qd/db`%qa`[secs()],%n;%:;nuked;nuked|%qs=%qt)Entry added.; @assert cor(fplayer(%q0,p),setq(p,%q0)[cand(nattr(%qd/DB`[edit(%qp,:,_)]`*),strmatch(%qs,))])[setq(a,edit(%qp,:,_))]=@pemit %#=ansi(r,Player %0 not found.); @assert strmatch(%qs,)=@pemit %#=attrib_set(%qd/db`%qa`[secs()],%n;%:;[name(%qp)];[get(%qp/lastsite)]|%qs=%qt)Entry added.; @assert strmatch(%qi,)=@pemit %#=switch(setr(e,extract(sort(edit(lattr(%qd/db`%qa`*),DB`%qa`,)),%qi,1)),,Entry not found.,[setq(l,get(%qd/DB`%qa`%qe))][center(> [if(isdbref(%qp),name(%qp),extract(first(%ql,|),3,1,;))] [if(setr(0,extract(first(%ql,|),4,1,;)),- %q0%b)]<,78,=)]%r[ljust(first(rest(%ql,|),=),35)] [ljust(timefmt($a $b $d,%qe),13)] [first(%ql,;)]%r[repeat(-,78)]%r[rest(%ql,=)]%r[repeat(=,78)]%r); think switch(nattr(%qd/db`%qa`*),0,pemit(%#,No entries found for [name(%qp)].),pemit(%#,center(> [if(isdbref(%qp),name(%qp),[extract(first(xget(%qd,xattr(%qd/DB`%qa`*,1,1)),|),3,1,;)])] <,78,=)%r[space(5)][ljust(Subject,36)][ljust(Date,14)]By%r[repeat(-,78)])[iter(sort(edit(lattr(%qd/db`%qa`*),DB`%qa`,)),pemit(%#,[ljust(inum(0),4)] [ljust(first(rest(setr(l,get(%qd/DB`%qa`%i0)),|),=),35)] [ljust(timefmt($a $b $d,%i0),13)] [first(%ql,;)]))][pemit(%#,repeat(=,78)%r)])

&CMD`AINFO`NOARG u(sgo)=$+oldainfo:think setq(d,#706); @pemit %#=[repeat(=,78)]%r[table([ljust(#,6)]Name|[ljust(#,6)]Name,38,78,|)]%r[repeat(-,78)]%r[table(iter(unique(edit(regeditall(lattr(%qd/db`*`*),%\bDB`%(%[^`%]+%)`%[0-9%]+%\b,$1),_,:)),ljust([nattr(%qd/DB`[edit(%i0,:,_)]`*)],6)[if(isdbref(%i0),name(%i0)%([first(%i0,:)]%),%i0%([extract(first(xget(%qd,xattr(%qd/DB`[edit(%i0,:,_)]`*,1,1)),|),3,1,;)]%))],,|),38,78,|)]%r[repeat(=,78)]%r

&CMD`AINFO/REMOVE u(sgo)=$+oldainfo/remove */*:think setq(d,#706);  think cor(fplayer(%0,p)[setq(a,edit(%qp,:,_))],setq(a,edit(%0,:,_))1); @break switch(setr(e,extract(sort(edit(lattr(%qd/db`%qa`*),DB`%qa`,)),%1,1)),,1)=@pemit %#=Entry not found. %qa; @pemit %#=center(> [name(%qp)] <,78,=)%r[ljust(first(rest(setr(l,get(%qd/DB`%qa`%qe)),|),=),35)] [ljust(timefmt($a $b $d,%qe),13)] [first(%ql,;)]%r[repeat(-,78)]%r[rest(%ql,=)]%r[repeat(=,78)]%r%rRemoved.[wipe(%qd/DB`%qa`%qe)]

&CMD`CHECKCHANLOCKS u(sgo)=$+checkchanlocks: @assert isadmin(%#); think null(iter(channels(|),if(setr(l,ulocal(%wa/u`check-channels,%i0)),pemit(%#,%i0: [itemize(iter(%ql,name(%i0),,|),|)]),),|))

&CMD`+GUESTPASS u(sgo)=$+guestpass *:@break findplayer(%0); @assert isint(setr(c,get(%qp/altinfo`caltsid)))=@pemit %#=ansi(r,Error: [name(%qp)] does not have +calts set.); @break get(#1911/pass`%qc)=@pemit %#=ansi(hx,Voiding [name(%qp)]'s guest pass.)[attrib_set(#1911/pass`%qc)][cemit(Y-Admin-Screaming,[ansi(hx,%n voided [name(%qp)]'s guest pass.)],1)]; @pemit %#=ansi(h,Approving [name(%qp)]'s guest pass.)[attrib_set(#1911/pass`%qc,1)][cemit(Y-Admin-Screaming,%n approved [name(%qp)]'s guest pass.,1)]

&CMD`GUESTPASSES u(sgo)=$+guestpasses:@pemit %#=%r= [ljust(CID,6,%b=%b)]Names [repeat(=,64)]%r[iter(edit(lattr(#1911/pass`*),PASS`,),%b%b[ljust(case(get(#1911/pass`%i0),2,ansi(hx,%i0),%i0),6)][itemize(iter(get(#2211/objid`%i0),name(%i0),,|),|)],,%r)]%r[repeat(-,78)]%rGrey IDs are automatically added entries from alts of admin/fachead.%r[repeat(=,78)]

&CMD`LASTPOSE u(sgo)=$+lastpose *:@break findplayer(%0); @assert hasattrval(%qp/ADMINATTRS`LASTPOSE)=@pemit %#=name(%qp) does not have a lastpose attribute yet.; @pemit %#=ansi(hx,ansi(nc,name(%qp)) last posed in [ansi(nc,squish(rest(setr(a,get(%qp/adminattrs`lastpose)),-)))] on [ansi(nc,convsecs(setr(t,squish(first(%qa,-)))))] - [squish(timestring(sub(secs(),%qt)))] ago)

&CMD`LONGCHECKPL u(sgo)=$+longcheckpl:think setq(c,iter(setr(l,search(all,type,player,elock,level:>34&!ooc_character:1&!flag^wizard&%(adminattrs`ic_character:1|!flag^royalty%))),get(%i0/altinfo`caltsid))); think setq(s,iter(setunion(%qc,,,n),elements(%ql,matchall(%qc,%i0)),,|)); think pemit(%#,repeat(=,50)); think iter(%qs,pemit(%#,[ljust(name(first(%i0)),20)][setq(0,iter(%i0,get(%i0/level)))][iter(setunion(%q0,,,n),%i0 - [words(matchall(%q0,%i0))],,%,%b)]),|); think pemit(%#,repeat(=,50));

&CMD`OWNER u(sgo)=$^(?s)\+owner(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@switch/inline gt(strlen(%3),0)=1,{th setq(loc,%2);th setq(newown,%3)},0,{th setq(loc,%l);th setq(newown,%2)};@assert setr(tz,namegrab(setr(tzs,children(#2820)),%q<loc>))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Target zone not found.;@include u(ccs)/INC`PARTIAL=%q<newown>,UNION|UNAFFILIATED|SYNDICATE|CONFEDERATE|CONTESTED,|,ZONES,choice,choice;&owner %q<tz>=capnames(%q<choice>);&owner`setby %q<tz>=%#;&owner`setwhen %q<tz>=secs();@remit %q<tz>=[ansi(h,Territory '[ansi(nc,name(%q<tz>))]' has been reassigned to:)] [ansi(hr,get(%q<tz>/owner))];@nspemit %#=u(RFN`MSGHEAD,ZONES) Territory [ansi(h,name(%q<tz>))] set to: %q<choice>;@include u(ccs)/INC`CMSG={Territory [ansi(h,name(%q<tz>))] assigned to: [capnames(%q<choice>)].},,ZONES
@set u(sgo)/CMD`OWNER=regexp

&CMD`+PLSEARCH u(sgo)=$+plsearch *:@pemit %#=[header(Level %0)]%r[table(iter(setr(l,sort(search(All,type,player,elock,level:%0),name)),name(%i0),,|),26,78,|,)]%r[header(Total: [words(%ql)])]

&CMD`+CALTS u(sgo)=$+calts *:@force/inline %#=+account %0
&CMD`+MALTS u(sgo)=$+malts *:@force/inline %#=+account %0

&CMD`CHECKPC u(sgo)=$+checkpc *:@break findplayer(%0); @pemit %#=ansi(h,[ansi(c,name(%qp))] is [if(setr(l,itemize(iter(checkpc(%qp),ansi(hr,%i0)))),missing %ql,ansi(hg,OK))].)

&CMD`LISTIDLE u(sgo)=$+listidle:think [setq(0,sub(secs(),mul(60,60,24,120)))][setq(1,sub(secs(),mul(60,60,24,30)))][setq(2,sub(secs(),mul(60,60,24,60)))]; think [setq(v,sort(search(all,type,player,flags,o!c,elock,!%(idle_character:1|ooc_character:1%)&lastseconds:<%q0),attr:lastseconds))]; think [setq(f,sort(search(all,type,player,flags,!o!c,elock,!%(idle_character:1|ooc_character:1%)&chartype:FC&lastseconds:<%q1),attr:lastseconds))]; think [setq(o,sort(search(all,type,player,flags,!o!c,elock,!%(idle_character:1|ooc_character:1%)&chartype:OC&lastseconds:<%q2),attr:lastseconds))]; think [setq(c,sort(search(all,type,player,flags,!o!c,elock,!%(idle_character:1|ooc_character:1%)&chartype:OFC&lastseconds:<%q2),attr:lastseconds))]; think setq(s,secs())[pemit(%#,center(< FC >,78,=))][iter(%qf,pemit(%#,ljust(name(%i0),22)[rjust([first(timestring(sub(%qs,get(%i0/lastseconds))))],6)] %b%b[get(%i0/series)]))][pemit(%#,center(< OFC >,78,=))][iter(%qc,pemit(%#,ljust(name(%i0),22)[rjust([first(timestring(sub(%qs,get(%i0/lastseconds))))],6)] %b%b[get(%i0/series)]))][pemit(%#,center(< OC >,78,=))][iter(%qo,pemit(%#,ljust(name(%i0),22)[rjust([first(timestring(sub(%qs,get(%i0/lastseconds))))],6)] %b%b[get(%i0/series)]))][pemit(%#,center(< Vacation >,78,=))][iter(%qv,pemit(%#,ljust(name(%i0),22)[rjust([first(timestring(sub(%qs,get(%i0/lastseconds))))],6)] %b%b[get(%i0/series)]))][pemit(%#,repeat(-,78))];@pemit/silent %#=[center(Rules: FC 30 days%, OC/OFC 60 days%, Vacation 120 days,78)]%r[center(Characters with attributes 'idle_character' or 'ooc_character' are ignored,78)]%r[repeat(=,78)]

&CMD`PLSTATS u(sgo)=$+plstats:@assert isadmin(%#);think setq(l,search(all,type,player,elock,level:>0)); think [setq(0,0)][setq(s,sort(setr(p,iter(%ql,get(%i0/level)))))][setq(t,iter(setunion(%qs,%qs),%i0: [setr(w,words(matchall(%qs,%i0)))][if(gt(%qw,%q0),setq(0,%qw)[setq(o,%i0)])],,|))]; @pemit/silent %#=%r[ljust(Mean:,7)] [lmath(mean,%qs)]%r[ljust(Stddev:,7)] [lmath(stddev,%qs)]%r[ljust(Mode:,7)] %qo%r[ljust(Median:,7)] [lmath(median,%qs)]%r%r[table(%qt,10,44,|)]%r

&CMD`+SCRY u(sgo)=$+scry *:@break if(hastype(setr(t,pmatch(%0)),player),0,pemit(%#,That is not a valid player.)1); @pemit %#=center(Combat Status of [name(%qt)],44,=)%r| %b %b %b %bHP: [ljust(u(#1247/uthp_now)/[u(#1247/uthp_max)],7)] [SPACE(6)] Status: [switch(1,lte(u(#1247/uthp_now),0),KOed,getsta(%qt,stun),Stun,Okay)] %b |%r| %b %b Skill: [ljust(getnum(%qt,ski),2)] [SPACE(10)] Evasion: [ljust(getnum(%qt,eva),2)] %b %b |%r|%b Strength: [ljust(getnum(%qt,str),2)] [SPACE(8)] Endurance: [ljust(getnum(%qt,end),2)] %b %b |%r| %b %b %b Aid: [ljust(getnum(%qt,aid),2)][space(14)]Level: [ljust(u(#1247/u_lvl,%qt),5)]%b%b|%r| %b %b +miss: [if(getsta(%#,miss),No%b,Yes)] [SPACE(10)] +allow: [if(u(uallow),Yes,No%b)] %b %b|%r|[center([setq(x,default(%qt/COMBAT`DEANDBUFFDUR,0))]Buffed : [if(gt(%qx,0),BUFFED for %qx actions.)][if(lt(%qx,0),DEBUFFED for [mul(-1,%qx)] actions.)][if(eq(%qx,0),No.)],42)]|%r|[center(Epic Buffs : [switch([default(*%0/combat`ubuff-att,0)][default(*%0/combat`ubuff-def,0)],11,Both,10,Attack,01,Defense,None)],42)]|%r|[center(Signature and Other Info,42,-)]|%r|[center(Attack Buff : [mul(100,[default([pmatch(%0)]/combat`legacy`tdata`sgatkbuff,0)])]%%,21,)][center(Defense Buff : [mul(100,[default([pmatch(%0)]/combat`legacy`tdata`sgDefbuff,0)])]%%,21,)]|%r|[center(Buff Falloff : [mul(100,[default([pmatch(%0)]/combat`legacy`tdata`sgbufffall,0)])],22,)][center([if([default([pmatch(%0)]/combat`authority,0)],[ansi(hy,<<A>>)],[ansi(hx,<<A>>)])],10)][center([if([default([pmatch(%0)]/combat`legacy`bosson,0)],[ansi(hr,<<B>>)],[ansi(hx,<<B>>)])],10)]|%r[if([default([pmatch(%0)]/combat`authority,0)],|[center(Authority Mode:[space(5)] Global: [mul(100,[u([pmatch(%0)]/combat`authority)])]%%,42)]|%r)][if([default([pmatch(%0)]/combat`legacy`bosson,0)],|[center(Boss Mode:[space(5)]Attack: [mul([u([pmatch(%0)]/combat`legacy`bossatk)],100)]%% Defense: [mul([u([pmatch(%0)]/combat`legacy`bossdef)],100)]%%,42)]|%r)][repeat(=,44)]

&CMD`+SETMAXSTATS u(sgo)=$+setmaxstats *=*:@break not(isadmin(%#)); @break findplayer(%0); @break case(1,[strmatch(%1,clear)],pemit(%#,ansi(h,Maxstats cleared for [name(%qp)].))[wipe(%qp/stats`max*)]1,[regmatch(%1,^(%\d+) (%\d+) (%\d+) (%\d+) (%\d+)$,-1 0 1 2 3 4)],0,pemit(%#,ansi(r,Invalid arguments. Provide 5 numbers separated by spaces.))1); @pemit %#=ansi(h,Set maxstats for [name(%qp)]. Skill: [ansi(hx,%q0)] Strength: [ansi(hx,%q1)] Evasion: [ansi(hx,%q2)] Endurance: [ansi(hx,%q3)] Aid: [ansi(hx,%q4)]); think [set(%qp,stats`maxskill:%q0)][set(%qp,stats`maxstrength:%q1)][set(%qp,stats`maxevasion:%q2)][set(%qp,stats`maxendurance:%q3)][set(%qp,stats`maxaid:%q4)]

&CMD`+SETMINSTATS u(sgo)=$+setminstats *=*:@break not(isadmin(%#)); @break findplayer(%0); @break case(1,[strmatch(%1,clear)],pemit(%#,ansi(h,Minstats cleared for [name(%qp)].))[wipe(%qp/stats`min*)]1,[regmatch(%1,^(%\d+) (%\d+) (%\d+) (%\d+) (%\d+)$,-1 0 1 2 3 4)],0,pemit(%#,ansi(r,Invalid arguments. Provide 5 numbers separated by spaces.))1); @pemit %#=ansi(h,Set minstats for [name(%qp)]. Skill: [ansi(hx,%q0)] Strength: [ansi(hx,%q1)] Evasion: [ansi(hx,%q2)] Endurance: [ansi(hx,%q3)] Aid: [ansi(hx,%q4)]); think [set(%qp,stats`minskill:%q0)][set(%qp,stats`minstrength:%q1)][set(%qp,stats`minevasion:%q2)][set(%qp,stats`minendurance:%q3)][set(%qp,stats`minaid:%q4)]

&CMD`+STAFFTITLE u(sgo)=$+STAFFTITLE *:@force/inline %#=+channel/stafftag %0

&CMD`THEMEPLS u(sgo)=$+themepls:@nspemit %#=header(PLs by Theme);@dolist setr(themes,sortkey(#lambda/get(u(tdb)/\%0),reglattr(u(tdb)/^\\d+$),i))={@nspemit %#=get(u(tdb)/%i0): [iter(get(u(tdb)/%i0`CAST),default(%i0/level,??))];@switch/inline eq(inum(0),words(%q<themes>))=1,{@nspemit %#=header()}}

&CMD`THEMEPLS-ARG u(sgo)=$+themepls *:@nspemit %#=header(PLs for Themes: %0);@dolist setr(themes,sortkey(#lambda/get(u(tdb)/\%0),grepi(u(tdb),*,%0),i))={@nspemit %#=get(u(tdb)/%i0): [iter(get(u(tdb)/%i0`CAST),default(%i0/level,??))];@switch/inline eq(inum(0),words(%q<themes>))=1,{@nspemit %#=header()}}

&U_IDLEL u(sgo)=filter(#lambda/gt(sub(secs(),convtime(u(\%0/LASTLOGOUT))) ,mul(2592000,%0)),lsearch(all,eplayer,\[not(u(##/idleignore))\]))
@set u(sgo)/U_IDLEL=no_command
&WA u(sgo)=#1263

&CMD`GUESTBAN u(sgo)=$+guestban *:@switch [orlflags(%#,royalty wizard)]=1,{@switch [haspower(*%0,guest)]=1,{think [setq(0,[get(*%0/lastsite)])];@boot *%0;@sitelock %q0=!guest;@pemit %#=Site banned: %q0;@mail Goddish=Guest ban used/Guest ban used against %0 - site %q0},{@pemit %#=That is not a guest.}},{@pemit %#=You are not authorized to use that command.}